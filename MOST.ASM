
;			ｱ｣｣｣｣｣｣｣｣｣｣｣｣｣｣ｵ
;			･ファイル ページャー ＭＯＳＴ･
;			･     by Ken/ichiro(OPA)     ･
;			ｹ｣｣｣｣｣｣｣｣｣｣｣｣｣｣ｽ
IDEAL
INCLUDE "STDIO.ASH"
INCLUDE "FILE.ASH"
INCLUDE "MOUSE.ASH"
INCLUDE "STRING.ASH"
INCLUDE "VRAM.ASH"

VSYNC_MARK	EQU	OFF		;画面右上に"VSYNC"と表示する
KEYBIOS_CALL	EQU	OFF		;キー入力をＢＩＯＳで行う
FDEBUG		EQU	OFF		;関数名を表示する

_VERSION	EQU	"1.19"

FNAMEBIT	EQU	6		;ファイル名ポインタ計算のシフト幅
MAXFILES	EQU	96		;最大ファイル数(2^FNAMEBITであること)
FNAMESIZE	EQU	64		;ファイル名バッファ幅
MAXLINE		EQU	30000		;１ファイルの最大行数
BACK		EQU	-1
NEXT		EQU	1
TVRAMSIZE	EQU	0C00H		;テキストＶＲＡＭの大きさ（ワード）

TMP		=	0

	PSTART

	MACRO	_$LBL	NUM		;ここはデバッグ用のマクロなんだな
	LABEL	_$L&NUM	BYTE		;FDEBUGを参照。
	ENDM

	MACRO	_$PUT	NUM
		VPUTS	60,0,<AOF _$L&NUM>
	ENDM

	MACRO	FUNCN	NAME
	IF FDEBUG
	DEFS	DATA
		_$LBL	%TMP
		DB	"&NAME&  ",NULL
	ENDS
		PUSH	AX
		PUSH	DX
		_$PUT	%TMP
		POP	DX
		POP	AX
TMP		=	TMP + 1
	ENDIF
	ENDM

;************************************************
	DEFS	CODE

START:		SETSEG	DS,DGROUP
		MOV	AX,ES
		PUSH	DS
		POP	ES
		MOV	[PSP],AX
		GETARGS AX

		CALL	SETCOMSPEC
		XOR	DX,DX
		MOV	[EDITOR],DL
		MOV	[CLIP],DL
		CALL	SETMOSTENV 	;AX = PSP SEG

		MOV	AH,1AH		; = SETDTA DTA
		MOV	DX,AOF DTA
		DOSINT

		MOV	CX,MAXFILES		;現在行を初期化
		MOV	AX,1
		MOV	DI,AOF NOWTOPLINE
		CLD
	REP	STOSW

		MOV	CX,10*2			;マークを初期化
		MOV	AX,-1
		MOV	DI,AOF MARKBUF
;		CLD
	REP	STOSW

		XOR	AX,AX
		MOV	[SEARCHDIR],AX
		MOV	[SEARCHWORD+2],AL

		MOV	AX,3
		MOV	DX,32767
		MUSINT
		XOR	AX,AX
		CMP	DX,32767
		JE	@@1
		MOV	CX,320
		MOV	DX,200
		MOV	AX,4
		MUSINT
		MOV	AX,65535
@@1:		MOV	[MOUSE],AX

		MOV	AX,4400H
		XOR	BX,BX	;BX<=STDIN
		DOSINT
		TEST	DX,0000000010000000B
		JNZ	CHARDEV

		INC	[FILES]			;標準入力がブロックデバイス
		MOV	[FNAME+0],NULL
		MOV	[TABSIZES+0],8
		JMP	CHKARGV_OK

CHARDEV:	XOR	CX,CX
		CMP	[ARGC],CL
		JNE	CHKARGV
		INC	[ARGC]
		MOV	BX,AOF ARGV		;力技！！
		MOV	[WORD BX],(AOF ARGV)+2
		MOV	BX,[BX]
		MOV	[WORD BX],'*' ; '*' NULL

CHKARGV:	CMP	[ARGC],CL
		JE	CHKARGV_OK
		PUSH	CX

		SHL	CX,1
		MOV	BX,CX
		MOV	BX,[AOF ARGV+BX]
		MOV	AL,[BX]

;		CMP	AL,'-'			;スイッチ文字か
;		JE	ARG_SW
		CMP	AL,'/'
		JNE	ARG_FILE

ARG_SW:		INC	BX			;/?か
		MOV	AL,[BX]
		CMP	AL,'?'
		JNE	ARG_HELP

		PUTS	<AOF CREDIT>
		PUTS	<AOF USASE_MES>

		CMP	[MOUSE],NO
		JE	@@2

		PUTS	<AOF CANMOUSE_MES>
@@2:		RETURN	1

ARG_HELP:	CALL	HELP			;/○の時
		RETURN	1

ARG_FILE:	STRCPY	<AOF FWORK>,BX		;ファイル名の時

		MOV	SI,AOF FWORK
		CALL	GETFN			;ファイル名の＊文字補完

		STRCHR	<AOF FWORK>,'.'		;拡張子があるか
		TEST	DI,DI
		JNZ	EXT_ARU

		CMP	[MOSTEXTNUM],0		;補完候補がない
		JE	EXT_ARU

		MOV	DI,AOF FWORK		;文字列の最後をポイント
		CALL	_STRLEN
		MOV	[BYTE DI-1],'.'

		XOR	CX,CX			;拡張子補完
		XOR	AX,AX
EXT_LOOP:	CMP	CX,[MOSTEXTNUM]
		JE	EXT_END
		PUSH	AX	;あったか
		PUSH	CX	;いくつめか
		PUSH	DI	;ポインタ

		MOV	SI,CX
		SHL	SI,2
		ADD	SI,AOF MOSTEXT
		STRCPY	DI,SI
		CALL	FINDFILE

		POP	DI
		POP	CX
		POP	AX
		JC	@@1
		INC	AX
@@1:		INC	CX
		JMP	SHORT EXT_LOOP

EXT_END:	DEC	DI
		MOV	[BYTE DI],NULL
		TEST	AX,AX
		JZ	NOEX
		JMP	SHORT ARG_NEXT

EXT_ARU:	CALL	FINDFILE
		JNC	ARG_NEXT

NOEX:		PUTS	<AOF NO_EXIST_MES>	;１つも見つからない
		PUTS	<AOF FWORK>
		PUTS	<AOF KOKKA_MES>

		RETURN	1

ARG_NEXT:	POP	CX
		INC	CX
		JMP	CHKARGV
CHKARGV_OK:
		XOR	AX,AX			;キーバッファビープの無視
		MOV	ES,AX
		MOV	BX,500H
		MOV	AL,[ES:BX]

		OR	AL,20H
		MOV	[ES:BX],AL
		PUSH	DS
		POP	ES

		CALL	PUSHTVRAM
		CALL	PUSH_VECT

DO:		CALL	DOFILE			;ここがメインか？
		CMP	AX,-1
		JE	MAIN_QUIT
		MOV	[FN],AX
		JMP	SHORT DO

MAIN_QUIT:					;正常終了
		CALL	POP_VECT
		CALL	POPTVRAM

		CALL	KEYFLUSH
		RETURN	0

;************************************************
	PROC	FINDFILE	NEAR		;ファイルを検索しリストに追加

		STRCHR	<AOF FWORK>,'*'
		TEST	DI,DI
		JNZ	WILDFILE
		STRCHR	<AOF FWORK>,'?'
		TEST	DI,DI
		JNZ	WILDFILE

		H_OPEN	<AOF FWORK>,00100000B	;ワイルドカード文字がない
		JC	ARG_NOEX
		H_CLOSE	AX

		CMP	[FILES],MAXFILES
		JE	FILE_END

		MOV	DI,AOF FNAME
		MOV	BX,[FILES]
		PUSH	BX
		INC	[FILES]
		SHL	BX,FNAMEBIT
		ADD	DI,BX
		PUSH	DI
		STRCPY	DI,<AOF FWORK>
		POP	DI
		POP	BX
		CALL	SETTABSIZES
		CLC
		RET

ARG_NOEX:	STC
		RET

WILDFILE:	GETPATH	MYBUF,FWORK		;ワイルドカード文字がある
		F_SEARCH FWORK,06H

		JC	ARG_NOEX

ARG_EXIST:	CMP	[FILES],MAXFILES
		JE	FILE_END		;ファイルが多すぎる

		MOV	DI,AOF FNAME
		MOV	BX,[FILES]
		PUSH	BX
		INC	[FILES]
		SHL	BX,FNAMEBIT
		ADD	DI,BX
		MOV	SI,AOF MYBUF
		STRCPY	DI,SI
		PUSH	DI
		MOV	SI,AOF DTA + 1EH
		STRCPY	DI,SI
		POP	DI
		POP	BX
		CALL	SETTABSIZES

		N_SEARCH
		JNC	ARG_EXIST

FILE_END:	CLC
		RET
	ENDP

;************************************************
	PROC	DOFILE	NEAR			;主役ルーチン
		FUNCN	DOFILE

		CALL	OPEN_FILE

		MOV	DX,AOF WAIT_MES
		CALL	PUTMES
;		CALL	VCLS30
		CALL	PUTPAGE
		CALL	PUTSTAT

LOOP_TOP:
		CALL	BGETC			;********************
		FUNCN	DO_LOOP

DO_LOOP2:	MOV	[INKEY],AX
		CMP	AX,3600H		;次ページ
		JE	D_N_PAGE
		CMP	AH,2BH
		JNE	KSEL_2

		CMP	[QFLAG],0		;^Q が前にあれば
		JE	NOLAST			;ファイル末端

		MOV	AX,[NL]
		ADD	AX,[LINES2]
		CMP	AX,[FMAXLINE]
		JNB	PASS

		CALL	MARK0

		MOV	AX,[FMAXLINE]
		CMP	AX,[READMAXLINE]
		JE	@@4
		MOV	DX,AOF WAIT_MES
		CALL	PUTMES

		MOV	[BYTE TABCFLAG],0
		MOV	DX,[READMAXLINE]
@@3:		MOV	AX,[FMAXLINE]
		INC	AX
		CMP	DX,AX
		JNBE	@@4

		PUSH	DX
		CALL	READLINE
		POP	DX
		INC	DX
		JMP	SHORT @@3

@@4:		MOV	[BYTE TABCFLAG],1
		MOV	AX,[FMAXLINE]
		INC	AX
		CMP	AX,[LINES]
		JNB	@@5

		MOV	[NL],1
		JMP	SHORT @@6

@@5:		MOV	DX,[FMAXLINE]
		SUB	DX,[LINES2]
		MOV	[NL],DX

@@6:		CALL	PUTPAGE
		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		JMP	DO_LOOP

NOLAST:		CMP	AX,2B03H
		JNE	KSEL_2

D_N_PAGE:	MOV	DX,[NL]			;次頁
		ADD	DX,[LINES2]
		CMP	DX,[FMAXLINE]
		JNB	PASS

		MOV	DX,[LINEH]
		ADD	[NL],DX
		MOV	DX,[NL]
		ADD	DX,[LINES2]
		CMP	DX,[READMAXLINE]
		JBE	@@N1

		MOV	CX,DX
		MOV	[BYTE TABCFLAG],0
		MOV	DX,[READMAXLINE]
@@N2:		INC	DX
		PUSH	CX
		PUSH	DX
		CALL	READLINE
		POP	DX
		POP	CX
		CMP	CX,DX
		JNE	@@N2

@@N1:		MOV	[BYTE TABCFLAG],1
		CMP	DX,[FMAXLINE]
		JB	@@N3

		MOV	DX,[FMAXLINE]
		SUB	DX,[LINES2]
		MOV	[NL],DX

@@N3:		CALL	PUTPAGE

		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		JMP	DO_LOOP

KSEL_2:		CMP	AX,3700H		;前ページ
		JE	D_B_PAGE
		CMP	AH,13H
		JNE	KSEL_31

		CMP	[QFLAG],0
		JE	NOTOP

		CMP	[NL],1
		JNE	@@W1

		MOV	[SEARCHLINE],1
		JMP	PASS

@@W1:		CALL	MARK0
		MOV	AX,1
		MOV	[NL],AX
		MOV	[SEARCHLINE],AX

		CALL	PUTPAGE
		JMP	DO_LOOP

NOTOP:		CMP	AX,1312H
		JNE	KSEL_31

D_B_PAGE:	CMP	[NL],1
		JE	PASS

		MOV	DX,[NL]
		SUB	DX,[LINEH]
		JBE	@@@D1
		CMP	DX,1
		JNBE	@@@D2
@@@D1:		MOV	DX,1
@@@D2:		MOV	[NL],DX
		CALL	PUTPAGE

		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		JMP	DO_LOOP

KSEL_31:	CMP	AX,0291AH		;^Z
		JE	S_N_LINES
;		JMP	SHORT KSEL_3

KSEL_3:		MOV	[S_FLAG1],0

		CMP	AX,2A18H		;次行
		JE	S_N_LINE1
		CMP	AX,3D00H
		JNE	KSEL_41

		MOV	[S_FLAG1],-1

		CALL	GETSFT
		TEST	AL,10H
		JZ	S_N_LINE1

S_N_LINES:	XOR	AL,AL
		CALL	S_NEXTLINE
		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		TEST	AX,AX
		JZ	DO_LOOP

		CALL	KBHIT
		TEST	BH,BH
		JZ	S_N_LINES
		JMP	DO_LOOP

S_N_LINE1:	CALL	GETSFT
		TEST	AL,1
		JNZ	D_N_LINE

S_N_@1:		MOV	AL,[DEF_SPEED]
		CALL	S_NEXTLINE

		CALL	GETSFT
		TEST	AL,1
		JNZ	D_N_LINE

		CMP	[S_FLAG1],0
		JE	S_N_@2

		TEST	AL,16
		JNZ	S_N_LINES

S_N_@2:		CALL	CHKTBL_DOWN
		JNZ	S_N_@1

		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		JMP	DO_LOOP

D_N_LINE:	MOV	AL,[DEF_SPEED+1]
		CALL	S_NEXTLINE
		CALL	GETSFT
		TEST	AL,1
		JNZ	D_N_LINE

		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		JMP	DO_LOOP

KSEL_41:	CMP	AX,01117H		;^W
		JE	S_B_LINES
;		JMP	SHORT KSEL_4

KSEL_4:		MOV	[S_FLAG1],0

		CMP	AX,1205H		;前行
		JE	S_B_LINE1
		CMP	AX,3A00H
		JNE	KSEL_51

		MOV	[S_FLAG1],-1

		CALL	GETSFT
		TEST	AL,10H
		JZ	S_B_LINE1

S_B_LINES:	XOR	AL,AL
		CALL	S_BACKLINE
		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		TEST	AX,AX
		JZ	DO_LOOP

		CALL	KBHIT
		TEST	BH,BH
		JZ	S_B_LINES
		JMP	DO_LOOP

S_B_LINE1:	CALL	GETSFT
		TEST	AL,1
		JNZ	D_B_LINE

S_B_@1:		MOV	AL,[DEF_SPEED]
		CALL	S_BACKLINE

		CALL	GETSFT
		TEST	AL,1
		JNZ	D_B_LINE

		CMP	[S_FLAG1],0
		JE	S_B_@2

		TEST	AL,16
		JNZ	S_B_LINES

S_B_@2:		CALL	CHKTBL_UP
		JNZ	S_B_@1

		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		JMP	DO_LOOP

D_B_LINE:	MOV	AL,[DEF_SPEED+1]
		CALL	S_BACKLINE
		CALL	GETSFT
		TEST	AL,1
		JNZ	D_B_LINE

		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		JMP	DO_LOOP

KSEL_51:	CMP	AX,03420H		;[SPACE]による次のページ
		JNE	KSEL_52

		MOV	AX,[LINES]
K5L:		DEC	AX
		JZ	K5E

		PUSH	AX
		MOV	AL,[DEF_SPEED]
		CALL	S_NEXTLINE
		POP	AX

		JMP	SHORT K5L

K5E:		CALL	KEYFLUSH
		JMP	DO_LOOP

KSEL_52:	CMP	AX,00F09H		;[TAB]
		JNE	KSEL_5

		CALL	CHGTAB
		JMP	DO_LOOP

KSEL_5:		CMP	AX,230AH		;マーク
		JNE	KSEL_6

		CALL	MARK
		JMP	DO_LOOP

KSEL_6:		CMP	AX,6400H		;ジャンプ
		JNE	KSEL_7

		CALL	JUMP
		CMP	AX,-1
		JE	DO_LOOP
		CMP	AX,[FN]
		JNE	JQ
		CALL	PUTPAGE
		JMP	DO_LOOP
JQ:		PUSH	AX
		PUSH	ES
		H_CLOSE	[HANDLE]
		FREEMEM	[HEAP]
		POP	ES
		POP	AX
		RET

KSEL_7:		CMP	AX,3800H		;速度アップ
		JNE	KSEL_75

		CALL	GETSFT
		MOV	BX,AOF DEF_SPEED
		TEST	AL,10H
		JZ	@@U1
		INC	BX
		INC	BX
@@U1:		TEST	AL,01H
		JZ	@@U2
		INC	BX
@@U2:
		MOV	AL,[BX]
		CMP	AL,16
		JAE	DO_LOOP
		SHL	AL,1
		MOV	[BX],AL
		JMP	DO_LOOP

KSEL_75:	CMP	AX,3900H		;速度ダウン
		JNE	KSEL_85

		CALL	GETSFT
		MOV	BX,AOF DEF_SPEED
		TEST	AL,10H
		JZ	@@D1
		INC	BX
		INC	BX
@@D1:		TEST	AL,01H
		JZ	@@D2
		INC	BX
@@D2:
		MOV	AL,[BX]
		CMP	AL,1
		JBE	DO_LOOP
		SHR	AL,1
		MOV	[BX],AL
		JMP	DO_LOOP

KSEL_85:	CMP	[QFLAG],0		;^QO (OS SHELL)
		JE	KSEL_8
		CMP	AH,18H
		JE	GOTOCH

KSEL_8:		CMP	AX,27			;終了
		JE	OWARI

		CMP	AX,1C0DH
		JNE	KSEL_9

		CALL	GETSFT
		TEST	AL,1
		JNZ	GOTOEDIT
		TEST	AL,10H
		JZ	OWARI

GOTOCH:						;子プロセス
		MOV	BX,[NOW_FNAME]
		CMP	[BYTE BX],NULL
		JE	DO_LOOP
		CALL	CHILD
		MOV	AX,[FN]
		RET

GOTOEDIT:	CMP	[EDITOR],NULL 		;エディタ起動
		JE	DO_LOOP
		MOV	BX,[NOW_FNAME]
		CMP	[BYTE BX],NULL
		JE	DO_LOOP

		CALL	C_EDITOR
		MOV	AX,-1
		CMP	[EDIT_QUIT],OFF
		JNE	@@GOE
		MOV	AX,[FN]
@@GOE:		RET

OWARI:		CMP	[QFLAG],0
		JNE	DO_LOOP
		CALL	CLOSE_FILE
		MOV	AX,-1
		RET

KSEL_9:		CMP	AX,3E00H		;次のファイル
		JNE	KSEL_10

		MOV	AX,[FN]
		INC	AX
		CMP	AX,[FILES]
		JNB	PASS

		CALL	CLOSE_FILE

		MOV	AX,[FN]
		INC	AX
		RET

KSEL_10:	CMP	AX,0AE00H		;前のファイル
		JNE	KSEL_11

		MOV	AX,[FN]
		TEST	AX,AX
		JBE	PASS

		CALL	CLOSE_FILE

		MOV	AX,[FN]
		DEC	AX
		RET

KSEL_11:	CMP	AX,6300H		;[F02] ファイル選択
		JNE	KSEL_115
		CALL	SEL_FILE
		JMP	DO_LOOP

KSEL_115:	CMP	AX,6700H		;クリップ
		JE	CLP
		CMP	AH,19H	;P's keycode
		JNE	KSEL_12

CLP:		CMP	[CLIPFLAG],0
		JE	DO_LOOP		;DO ３連発
		CALL	DO_CLIP
		JMP	DO_LOOP

KSEL_12:	CMP	AX,0C1EH		;↓検索
		JNE	KSEL_13

		CALL	INPUTSTR
		TEST	AX,AX

		JNZ	S_F
		CALL	PUTPAGE
		JMP	DO_LOOP

S_F:		CALL	MARK0
		MOV	AX,NEXT
		MOV	[SEARCHDIR],AX

		MOV	AX,[NL]
		MOV	[SEARCHLINE],AX
		CALL	SEARCH
		TEST	AX,AX
		JZ	FIND_ERR
		JMP	FIND_OK

KSEL_13:	CMP	AX,0D1CH		;↑検索
		JNE	KSEL_14

		CALL	INPUTSTR
		TEST	AX,AX

		JNZ	S_B
		CALL	PUTPAGE
		JMP	DO_LOOP

S_B:		CALL	MARK0
		MOV	AX,BACK
		MOV	[SEARCHDIR],AX

		MOV	AX,[NL]
		DEC	AX
		MOV	[SEARCHLINE],AX
		CALL	SEARCH
		TEST	AX,AX
		JZ	FIND_ERR
		JMP	FIND_OK

KSEL_14:	CMP	AX,6600H		;同じ文字列を↓検索
		JNE	KSEL_15
		MOV	[SEARCHDIR],NEXT
		JMP	SHORT S_N
KSEL_15:	CMP	AX,8600H		;同じ文字列を↑検索
		JNE	KSEL_16
		MOV	[SEARCHDIR],BACK

S_N:		MOV	AL,[SEARCHWORD+2]
		TEST	AL,AL
		JZ	DO_LOOP

		MOV	AX,[SEARCHDIR]
		ADD	[SEARCHLINE],AX
		CALL	SEARCH
		TEST	AX,AX
		JZ	FIND_ERR
		JMP	FIND_OK

KSEL_16:	CMP	AX,3F00H		;ヘルプ
		JNE	KSEL_161

		MOV	AX,[LINES]
		PUSH	AX
		CMP	AX,20
		JNE	@@NOSET25

		MOV	[LINES],25
		PUTS	<AOF SET_L25>

@@NOSET25:	CALL	VHELP
		MOV	DX,AOF WAITING_MES
		CALL	PUTMES

		CALL	BGETC
		POP	AX
		MOV	[LINES],AX
		CMP	AX,20
		JNE	KS16_A
		PUTS	<AOF SET_L20>
KS16_A:		CALL	VCLS30
		CALL	SPLIT
		CALL	PUTPAGE
		JMP	DO_LOOP

KSEL_161:	CMP	AX,2107H		;^G 画面モード切り換え
		JNE	KSEL_17

		CMP	[LINES],30
		JE	DO_LOOP

		MOV	DX,[LINES]
		CMP	DX,20
		JE	@@162
		MOV	AX,20
		MOV	DX,AOF SET_L20
		JMP	SHORT @@163
@@162:		MOV	AX,25
		MOV	DX,AOF SET_L25
@@163:		PUTS	DX
		CALL	SETLINES

		CALL	PUTPAGE
		JMP	DO_LOOP

KSEL_17:	CMP	AH,0FFH			;マウス次の行
		JNE	KSEL_18
		CALL	S_NEXTLINE
		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		TEST	AX,AX
		JZ	MOUSE_STOP
		JMP	DO_LOOP

KSEL_18:	CMP	AH,0FEH			;マウス前の行
		JNE	KSEL_19
		CALL	S_BACKLINE
		MOV	DX,[NL]
		MOV	[SEARCHLINE],DX
		TEST	AX,AX
		JZ	MOUSE_STOP
;		JMP	DO_LOOP

						;*********************

KSEL_19:
DO_LOOP:	CALL	QCHK
		JMP	LOOP_TOP

PASS:		CALL	PUTSTAT 		;コマンドが無視された
		JMP	DO_LOOP

MOUSE_STOP:	MOV	DX,200			;マウスを真ん中に戻す
		MOV	AX,4
		MUSINT
		JMP	DO_LOOP

FIND_OK:	CALL	PUTSEARCHPAGE		;検索成功
		CALL	BGETC
		MOV	[INKEY],AX
		PUSH	AX
		CALL	QCHK
		MOV	AX,V_WHITE
		CALL	SEARCH_ATT
		POP	AX
		JMP	DO_LOOP2

FIND_ERR:	CALL	PUTPAGE 		;検索失敗
		PUTC	BEEP
		MOV	DX,AOF CANT_FIND_MES
		CALL	PUTMES
		JMP	DO_LOOP

	ENDP

;************************************************
	PROC	QCHK	NEAR			;^Q フラグの設定
		FUNCN	QCHK

		PUSH	ES
		PUSH	AX
		MOV	AX,0A000H
		MOV	ES,AX
		CMP	[INKEY],1011H
		JNE	NO_Q
		CMP	[QFLAG],0
		JNE	NO_Q
		MOV	[QFLAG],-1
		MOV	AX,'^Q'
		JMP	SHORT @@QQ
NO_Q:		MOV	[QFLAG],0
		MOV	AX,'  '
@@QQ:		MOV	[ES:(30*80+60)*2],AH
		MOV	[ES:(30*80+61)*2],AL
		POP	AX
		POP	ES
		RET
	ENDP

;************************************************
	PROC	PUTPAGE	NEAR			;１ページ表示
		FUNCN	PUTPAGE

		XOR	CX,CX
		MOV	DI,[NL]

PUTP_LOOP:	PUSH	CX
		PUSH	DI
		MOV	DX,DI
		CALL	READLINE
		POP	DI
		POP	CX
		VCLL	CX
		VPUTS	0,CL,<AOF MYBUF>
		INC	DI
		INC	CX
		CMP	CL,[BYTE LINES]
		JB	PUTP_LOOP

;		CALL	SPLIT
		CALL	ATTSET
		CALL	PUTSTAT

		RET

	ENDP

;************************************************
	PROC	NEXTLINE	NEAR		;次の行表示
		FUNCN	NEXTLINE

		MOV	DX,[NL]
		ADD	DX,[LINES]
		PUSH	DX
		CALL	READLINE
		MOV	[SS_ROLL],1
		CALL	VSYNCWAIT
		POP	DX
		INC	[NL]

		MOV	AX,[LINES]
		DEC	AX
		VCLL	AX
		VPUTS	0,AL,<AOF MYBUF>
		CALL	ATTSET
		CALL	PUTSTAT
		MOV	AX,-1
		RET

	ENDP

;************************************************
	PROC	S_NEXTLINE	NEAR		;スムース次の行
		FUNCN	S_NEXTLINE

		MOV	DX,[NL]
		ADD	DX,[LINES2]
		INC	DX
		CMP	DX,[FMAXLINE]
		JNA	@@X
		MOV	DX,[NL]
		CMP	DX,0
		JG	S_NXL_END

@@X:		TEST	AL,AL
		JNZ	@@2
		CALL	GETSFT
		TEST	AL,1
		JZ	@@1
		MOV	AL,[DEF_SPEED+3]
		JMP	SHORT @@2
@@1:		MOV	AL,[DEF_SPEED+2]
@@2:		CMP	AL,16
		JE	NEXTLINE		;CALL + RET
		CMP	[RASTER],20
		JNE	@@@1
		CMP	AL,8
		JNE	@@@1
		MOV	AL,10
@@@1:		MOV	[SS_STEP],AL
		MOV	[SS_COUNT],AL

		CALL	VSYNCWAIT

		PUSH	AX
		MOV	DX,[NL]
		ADD	DX,[LINES]
		PUSH	DX
		CALL	READLINE
		POP	DX
		INC	[NL]
		POP	AX

SS_UP:		ADD	AL,[SS_STEP]
		CMP	AL,[BYTE RASTER]
		JAE	SSU_END
		MOV	[SS_COUNT],AL
		CALL	VSYNCWAIT
		JMP	SHORT SS_UP

SSU_END:	MOV	[SS_COUNT],0
		MOV	[SS_ROLL],1
		CALL	VSYNCWAIT
		MOV	AX,[LINES]
		DEC	AX
		VCLL	AX
		VPUTS	0,AL,<AOF MYBUF>
		CALL	ATTSET
		CALL	PUTSTAT
		VSETC	V_WHITE
		MOV	AX,-1
		RET

S_NXL_END:	CALL	PUTSTAT
		XOR	AX,AX
		RET
	ENDP

;************************************************
	PROC	BACKLINE	NEAR		;前の行
		FUNCN	BACKLINE

		DEC	[NL]
		MOV	DX,[NL]
		CALL	READLINE

		MOV	[SS_ROLL],-2
		CALL	VSYNCWAIT

		VPUTS	0,0,<AOF MYBUF>
		CALL	ATTSET
		CALL	PUTSTAT
		MOV	AX,-1
		RET

	ENDP

;************************************************
	PROC	S_BACKLINE	NEAR		;スムース前の行
		FUNCN	S_BACKLINE

		MOV	DX,[NL]
		CMP	DX,1
		JBE	S_BKL_END

		TEST	AL,AL
		JNZ	@@3
		CALL	GETSFT
		TEST	AL,1
		JZ	@@1
		MOV	AL,[DEF_SPEED+3]
		JMP	SHORT @@3
@@1:		MOV	AL,[DEF_SPEED+2]
@@3:		CMP	AL,16
		JE	BACKLINE
		CMP	[RASTER],20
		JNE	@@@1
		CMP	AL,8
		JNE	@@@1
		MOV	AL,10
@@@1:		MOV	[SS_STEP],AL

		DEC	[NL]
		MOV	DX,[NL]
		CALL	READLINE

		CALL	RDN1
		MOV	AL,[BYTE RASTER]
		SUB	AL,[SS_STEP]
		MOV	[SS_COUNT],AL
		MOV	[SS_ROLL],-1
		CALL	VSYNCWAIT
		VPUTS	0,0,<AOF MYBUF>
		CALL	ATTSET

		MOV	AL,[BYTE RASTER]
		SUB	AL,[SS_STEP]
SS_DN:		SUB	AL,[SS_STEP]
		MOV	[SS_COUNT],AL
		CALL	VSYNCWAIT
		CMP	AL,0
		JLE	SSD_END
		JMP	SHORT SS_DN

SSD_END:
		MOV	[SS_COUNT],0
		CALL	PUTSTAT
		MOV	AX,-1
		RET

S_BKL_END:	CALL	PUTSTAT
		XOR	AX,AX
		RET
	ENDP

;************************************************
	PROC	PUTSTAT	NEAR			;スティタス表示
		FUNCN	PUTSTAT

		MOV	DI,AOF MESBUF

		CLD
		MOV	AL,' '
		STOSB
		MOV	AX,'--'
		STOSW

		MOV	SI,[NOW_FNAME]		;ファイル名
		STRCPY	DI,SI

		MOV	AX,'--'
		STOSW
		MOV	AL,' '
		STOSB

		PUSH	DI			;ＮＬ
		MOV	AX,[NL]
		ADD	AX,[LINES2]
		INC	AX
		INC	AX
		CMP	AX,[FMAXLINE]
		JB	DONL
		MOV	AX,[FMAXLINE]
DONL:		MOV	BP,AX			;レジスタなら何でもいいや
		WTODEC	AX,<AOF MYBUF>
		MOV	[BYTE DI],NULL
		POP	DI
		SUPCPY	DI,<AOF MYBUF>

		STRCPY	DI,<AOF LINE_MES>	;line:の文字

		PUSH	DI			;LINE_POINTER
		MOV	DX,BP
		CALL	GET_LP
		DWTODEC	[LP_BYTE+2],[LP_BYTE],<AOF MYBUF>
		XOR	AL,AL
		MOV	[MYBUF+10],AL
		POP	DI
		SUPCPY	DI,<AOF MYBUF>

		STRCPY	DI,<AOF BYTE_MES>	;byte:の文字

		MOV	DX,[LP_BYTE+2]		;％
		MOV	AX,[LP_BYTE]
		MOV	CX,[FILE_C]
		TEST	CX,CX
		JZ	@@2
		DIV	CX
		CMP	AL,100
		JBE	@@1
@@2:		MOV	AL,100

@@1:		BTODEC	AL,<AOF MYBUF>
		INC	BX
		MOV	[BYTE BX],NULL
		SUPCPY	DI,<AOF MYBUF>

		STRCPY	DI,<AOF CENT_MES>	;%) の文字

@@3:		MOV	[BYTE DI],SPC
		INC	DI
		CMP	DI,AOF MESBUF+80
		JB	@@3
		MOV	[BYTE DI],NULL

		VSETC	<V_CYAN + V_REV>
		VPUTS	0,30,<AOF MESBUF>
		VSETC	V_WHITE

		PUSH	ES			;インジケータ
		MOV	AX,0A000H
		MOV	ES,AX

		MOV	[BYTE ES:(30*80+62)*2],97H

		MOV	BX,AOF LP_BYTE
		MOV	DX,[BX+2]
		MOV	AX,[BX]
		MOV	BX,[FILE_D]
		TEST	BX,BX
		JZ	@@4
		DIV	BX
		CMP	AL,127
		JBE	@@5
@@4:		MOV	AL,127

@@5:		MOV	DX,AX
		SHR	AX,3
		MOV	DI,30*80+63
		ADD	DI,AX
		SHL	DI,1

		MOV	AX,DX
		AND	AX,0111B
		TEST	AX,AX
		JNE	@@6
		MOV	AX,20H
		JMP	SHORT @@7
@@6:		ADD	AX,87H

@@7:		MOV	[ES:DI+0],AX
		MOV	[ES:DI+2],AX
		ADD	DI,2000H
		AND	[BYTE ES:DI],11111011B
		POP	ES

		RET

	ENDP

;************************************************
	PROC	PUTMES	NEAR			;メッセージ表示
		FUNCN	PUTMES

		VSETC	<V_CYAN + V_REV>

		PUSH	DX
;		CALL	SPLIT
		VCLL	30
		POP	DX
		VPUTS	0,30,DX

		VSETC	V_WHITE
		RET

	ENDP

;************************************************
	PROC	MARK	NEAR			;マーク
		FUNCN	MARK

		MOV	DX,AOF MARKPROM
		CALL	PUTMES

		CALL	MARKSTAT
		CALL	BGETC

		CMP	AL,'0'
		JB	MARKEXIT
		CMP	AL,'9'
		JA	MARKEXIT

		XOR	AH,AH
		SUB	AL,'0'
		SHL	AX,2
		ADD	AX,AOF MARKBUF
		MOV	DI,AX

		MOV	[BYTE DI],NULL
		MOV	AX,[FN]
		MOV	[DI+1],AL
		MOV	AX,[NL]
		MOV	[DI+2],AX

MARKEXIT:	CALL	PUTSTAT
		RET
	ENDP

;************************************************
	PROC	JUMP	NEAR			;ジャンプ
		FUNCN	JUMP

		MOV	DX,AOF JUMPPROM
		CALL	PUTMES

		CALL	MARKSTAT
		CALL	BGETC

		CMP	AL,'0'
		JB	JUMPEXIT
		CMP	AL,'9'
		JA	JUMPEXIT

		XOR	AH,AH
		SUB	AL,'0'
		SHL	AX,2
		ADD	AX,AOF MARKBUF
		MOV	DI,AX

		CMP	[BYTE DI],-1
		JE	JUMPEXIT

		PUSH	[NL]
		PUSH	[FN]

		MOV	BX,[FN]
		SHL	BX,1
		ADD	BX,AOF NOWTOPLINE
		MOV	AX,[NL]
		MOV	[BX],AX

		XOR	DX,DX
		MOV	DL,[DI+1]
		MOV	BX,DX
		SHL	BX,1
		ADD	BX,AOF NOWTOPLINE
		MOV	AX,[DI+2]
		MOV	[BX],AX
		MOV	[NL],AX

		PUSH	DX	; MOV AX,DX ; PUSH AX
		CALL	PUTSTAT
		POP	AX
		MOV	[BYTE MARKBUF],0
		POP	DX	;[FN]
		MOV	[BYTE MARKBUF+1],DL
		POP	DX	;[NL]
		MOV	[MARKBUF+2],DX
		RET

JUMPEXIT:	CALL	PUTSTAT
		MOV	AX,-1
		RET
	ENDP

;************************************************
	PROC	MARKSTAT	NEAR		;マーク状況の表示
		FUNCN	MARKSTAT

		PUSH	ES
		MOV	AX,0A000H
		MOV	ES,AX

		MOV	DI,(80*30+15-1)*2
		MOV	SI,OFFSET MARKBUF
		XOR	BX,BX
		XOR	DX,DX

@@1:		MOV	AX,'-'
		CMP	[BYTE SI+BX],-1
		JE	@@2
		MOV	AL,'0'
		ADD	AX,DX
@@2:		TEST	DX,DX
		JZ	@@3
		MOV	[ES:DI],AX
		JMP	SHORT @@4
@@3:		MOV	[ES:(80*30+15+9)*2],AX
@@4:		INC	DX
		ADD	BX,4
		INC	DI
		INC	DI
		CMP	DX,10
		JNE	@@1

		POP	ES
		RET
	ENDP

;************************************************
	PROC	MARK0	NEAR			;現在位置をマーク０にマーク
		FUNCN	MARK0

		PUSH	AX
		MOV	[BYTE MARKBUF],0
		MOV	AX,[FN]
		MOV	[BYTE MARKBUF+1],AL
		MOV	AX,[NL]
		MOV	[MARKBUF+2],AX
		POP	AX
		RET
	ENDP

;************************************************
	PROC	INPUTSTR	NEAR		;検索文字列の入力
		FUNCN	INPUTSTR

		XOR	DX,DX
		MOV	AH,0EH
		INT	18H

		MOV	AX,[LINES]
		CMP	AX,30
		DEC	AX
		VCLL	AX
		PUSH	AX
		VSETC	<V_CYAN + V_REV>
		POP	AX
		DEC	AX
		VCLL	AX
		PUTS	<AOF PROMPT>
		CMP	[LINES],25
		JNE	@@@1
		PUTS	<AOF PROMPT1>	;25
		JMP	SHORT @@@2
@@@1:		CMP	[LINES],20
		JNE	@@@3
		PUTS	<AOF PROMPT2>	;20
		JMP	SHORT @@@2
@@@3:		PUTS	<AOF PROMPT3>	;30
@@@2:
		MOV	DX,AOF SEARCHWORD
		MOV	AX,0A48H
		MOV	[SEARCHWORD+0],AL
		DOSINT

		MOV	BX,AOF SEARCHWORD+2
		MOV	AL,[SEARCHWORD+1]
		CBW
		PUSH	AX
		ADD	BX,AX
		MOV	[BYTE BX],NULL

		VSETC	V_WHITE
		PUTS	<AOF PROM_2>
		MOV	AX,[LINES]
		DEC	AX
		VCLL	AX
		DEC	AX
		VCLL	AX
		MOV	DX,[NL]
		PUSH	AX
		ADD	DX,AX
		CALL	READLINE
		POSH	AX
		VPUTS	0,AL,<AOF MYBUF>
		POP	AX
		INC	AX
		MOV	DX,[NL]
		PUSH	AX
		ADD	DX,AX
		CALL	READLINE
		POP	AX
		VPUTS	0,AL,<AOF MYBUF>
		CALL	SPLIT
		POP	AX

		STRLEN	<AOF SEARCHWORD+2>

		RET

	ENDP

;************************************************
	PROC	SEARCH	NEAR			;検索本体
		FUNCN	SEARCH

		MOV	DX,AOF WAIT_MES
		CALL	PUTMES

		MOV	DX,[SEARCHLINE]

		TEST	DX,DX
		JZ	S_EXIT

		MOV	[BYTE TABCFLAG],0

SEARCH_LOOP:	PUSH	DX
		CALL	READLINE
		POP	DX

		CMP	DX,[FMAXLINE]
		JNBE	S_EXIT

		PUSH	DX
		STRISTR	<AOF MYBUF>,<AOF SEARCHWORD+2>
		POP	DX
		TEST	AX,AX
		JZ	SEARCH_NEXT

		MOV	[SEARCHLINE],DX
		MOV	BX,AOF MYBUF
		XOR	CX,CX
@@3:		CMP	BX,AX
		JAE	@@2
		CMP	[BYTE BX],TAB
		JNE	@@1

		MOV	DX,[TABSIZE]
		ADD	CX,DX
		DEC	DX
		NOT	DX
		AND	CX,DX
		DEC	CX

@@1:		INC	CX
		INC	BX
		JMP	SHORT @@3

@@2:		MOV	[SEARCHBYTE],CX
		MOV	DX,[SEARCHLINE]
		ADD	DX,[LINES]
		CALL	READLINE
		MOV	AX,-1
		MOV	[BYTE TABCFLAG],1
		RET

SEARCH_NEXT:	ADD	DX,[SEARCHDIR]
		TEST	DX,DX
		JZ	S_EXIT
		JMP	SHORT SEARCH_LOOP

S_EXIT:		SUB	DX,[SEARCHDIR]
		MOV	[SEARCHLINE],DX
		XOR	AX,AX
		MOV	[BYTE TABCFLAG],1
		RET
	ENDP

;************************************************
	PROC	PUTSEARCHPAGE	NEAR		;見つかったページを表示
		FUNCN	PUTSEARCHPAGE

		CMP	[SEARCHDIR],NEXT
		JNE	@@1

		MOV	DX,[SEARCHLINE]
		DEC	DX
		CMP	DX,[POS_LINE]
		JNBE	@@11
		MOV	DX,1		;ファイルの先頭にきわめて近いとき
		JMP	SHORT @@P
@@11:
		INC	DX
		SUB	DX,[NL]
		CMP	DX,[POS_LINE]
		JL	@@12
		MOV	AX,[LINES]
		DEC	AX
		DEC	AX
		SUB	AX,[POS_LINE]
		CMP	DX,AX
		JNG	@@QQ
@@12:
		MOV	DX,[SEARCHLINE]
		SUB	DX,[POS_LINE]
		JMP	SHORT @@P

@@1:		CMP	[SEARCHDIR],BACK
		JNE	@@Q

		MOV	DX,[SEARCHLINE]
		DEC	DX
		CMP	DX,[POS_LINE]
		JNBE	@@21
		MOV	DX,1
		JMP	SHORT @@P
@@21:
		INC	DX
		SUB	DX,[NL]
		CMP	DX,[POS_LINE]
		JL	@@22
		MOV	AX,[LINES]
		DEC	AX
		DEC	AX
		SUB	AX,[POS_LINE]
		CMP	DX,AX
		JNG	@@QQ
@@22:
		MOV	DX,[SEARCHLINE]
		ADD	DX,[POS_LINE]
		MOV	AX,[LINES2]
		INC	AX
		INC	AX
		SUB	DX,AX
		JNBE	@@23
		MOV	DX,1
@@23:
;		JMP	SHORT @@P

@@P:		MOV	AX,[FMAXLINE]
		SUB	AX,[LINES2]
		CMP	DX,AX
		JBE	@@P1
		MOV	DX,AX
@@P1:		CMP	[NL],DX
		JE	@@QQ
		MOV	[NL],DX
		CALL	PUTPAGE
		JMP	SHORT @@Q
@@QQ:		CALL	PUTSTAT
@@Q:		MOV	AX,V_YELLOW + V_REV
		JMP	SEARCH_ATT	; call + ret

	ENDP

;************************************************
	PROC	SETTABSIZES	NEAR		;BX:FN	DI:FILENAME
		FUNCN	SETTABSIZES		;TABSIZESの初期化

		ADD	BX,AOF TABSIZES
		MOV	[BYTE BX],8
		MOV	SI,DI
SETTAB_LOOP:	MOV	AL,[SI]
		INC	SI
		TEST	AL,AL
		JZ	SETTAB_EXIT
		CMP	AL,'.'
		JNE	SETTAB_LOOP

SETTAB_EXIT:	MOV	CX,[MOSTEXTNUM2]
		TEST	CX,CX
		JZ	@@Q
		MOV	DI,AOF MOSTEXT2

@@@1:		CALL	STRWCMP
		TEST	AL,AL
		JZ	SET_TAB4
		ADD	DI,4
		LOOP	@@@1
		JMP	SHORT @@Q

SET_TAB4:	MOV	[BYTE BX],4

@@Q:		RET
		ENDP

;************************************************
	PROC	CHGTAB	NEAR			;タブ間隔変更(4/8)
		FUNCN	CHGTAB

		MOV	DX,AOF WAIT_MES
		CALL	PUTMES

		XOR	BX,BX
		MOV	BX,[FN]
		ADD	BX,AOF TABSIZES
		CMP	[BYTE BX],8
		JNE	SETTO_8

		MOV	[BYTE BX],4
		MOV	[TABSIZE],4
		JMP	SHORT @@1

SETTO_8:	MOV	[BYTE BX],8
		MOV	[TABSIZE],8

@@1:		MOV	DX,[NL]
		CALL	GET_LP
		MOV	AX,[LP_BYTE]
		MOV	DX,[LP_BYTE+2]

		XOR	CX,CX			;タブ変更に伴う各種変数初期化
		MOV	[READMAXLINE],CX
		MOV	[NOWBYTE+0],CX
		MOV	[NOWBYTE+2],CX
		MOV	[LP_NOW_BYTE+0],CX
		MOV	[LP_NOW_BYTE+2],CX
		MOV	[LP_NOW_LINE],CX
		MOV	[NL],1
		MOV	[FMAXLINE],MAXLINE

		MOV	[BYTE TABCFLAG],0
@@2:		PUSH	DX
		PUSH	AX
		MOV	DX,[NL]
		CALL	READLINE
		MOV	DX,[NL]
		CALL	GET_LP
		POP	AX
		POP	DX

		MOV	BX,AOF LP_BYTE
		CMP	DX,[BX+2]
		JA	@@3
		CMP	AX,[BX]
		JNA	@@4

@@3:		INC	[NL]
		JMP	SHORT @@2

@@4:		MOV	[BYTE TABCFLAG],1
		CALL	PUTPAGE
		CALL	PUTSTAT

		RET
		ENDP

;************************************************
	PROC	HELP	NEAR			;キー操作一覧(TO STDOUT)
		FUNCN	HELP

		PUTSCR	<AOF PREHELP>

		MOV	BX,AOF HELP_MES
		MOV	AX,[MOUSE]
		OR	AX,AX
		JZ	NOMOUSE

		MOV	CX,19
		JMP	SHORT DOHELP

NOMOUSE:	MOV	CX,15
DOHELP:		PUTSCR	[BX]
		INC	BX
		INC	BX
		LOOP	DOHELP

		RET

	ENDP

;************************************************
	PROC	VHELP	NEAR			;キー操作一覧(TO TVRAM)
		FUNCN	VHELP

		MOV	AX,[TABSIZE]
		PUSH	AX
		MOV	AX,8
		MOV	[TABSIZE],AX

		CALL	VCLS30
		STRCPY	<AOF MYBUF>,<AOF PREHELP>
		MOV	BX,AOF MYBUF
		CALL	TABCONV
		VPUTS	0,1,<AOF MYBUF>

		MOV	BX,AOF HELP_MES
		MOV	AX,[MOUSE]
		OR	AX,AX
		JZ	VNOMOUSE

		MOV	CX,19
		JMP	SHORT @@1

VNOMOUSE:	MOV	CX,15
@@1:		MOV	AL,3
VDOHELP:	PUSH	BX
		PUSH	CX
		PUSH	AX
		STRCPY	<AOF MYBUF>,[BX]
		MOV	BX,AOF MYBUF
		CALL	TABCONV
		POSH	AX
		VPUTS	0,AL,<AOF MYBUF>
		POP	AX
		POP	CX
		POP	BX
		INC	BX
		INC	BX
		INC	AL
		LOOP	VDOHELP

		POP	AX
		MOV	[TABSIZE],AX

		RET
	ENDP

;************************************************
	PROC	OPEN_FILE	NEAR		;ファイルをオープン・その他諸々
		FUNCN	OPEN_FILE

		MOV	BX,[FN]
		SHL	BX,FNAMEBIT
		ADD	BX,AOF FNAME
		MOV	[NOW_FNAME],BX

		MOV	BX,[FN]
		SHL	BX,1
		MOV	BX,[AOF NOWTOPLINE+BX]
		MOV	[NL],BX
		MOV	[SEARCHLINE],BX

		CMP	[BYTE FNAME+0],NULL
		JNE	_OPEN_FILE

		H_DUP	STDIN			;対象がＣＯＮ
		JC	CANNOT_OPEN
		MOV	[HANDLE],AX

		H_OPEN	<AOF CON_PATH>
		JC	CANNOT_OPEN

		H_DUP2	STDIN,AX
		JC	CANNOT_OPEN
		JMP	SHORT OPEN_OK

_OPEN_FILE:	MOV	DX,[NOW_FNAME]		;対象がファイル
		H_OPEN	DX,00100000B
		JC	CANNOT_OPEN
		MOV	[HANDLE],AX
		JMP	SHORT OPEN_OK

CANNOT_OPEN:	CALL	POP_VECT
		CALL	POPTVRAM

		PUTS	<AOF CANNOT_OPEN_MES>

		MOV	DX,[NOW_FNAME]
		PUTS	DX
		PUTS	<AOF KOKKA_MES>
		RETURN	1

OPEN_OK:	MOV	BX,[FN]		;タブ幅セット
		ADD	BX,AOF TABSIZES
		XOR	AX,AX
		MOV	AL,[BX]
		MOV	[TABSIZE],AX

		GETFLEN [HANDLE]
		JC	DISK_ERROR
		MOV	[FILELEN+0],AX		;各種変数初期化
		MOV	[FILELEN+2],DX

		PUSH	AX
		PUSH	DX

		MOV	BX,100
		DIV	BX
		MOV	[FILE_C],AX		;％用変数

		POP	DX
		POP	AX

		MOV	BX,127
		DIV	BX
		MOV	[FILE_D],AX		;グラフゲージ用変数

		XOR	AX,AX
		MOV	[READMAXLINE],AX

		MOV	[NOWBYTE+0],AX
		MOV	[NOWBYTE+2],AX

		MOV	[LP_NOW_BYTE+0],AX
		MOV	[LP_NOW_BYTE+2],AX
		MOV	[LP_NOW_LINE],AX

		MOV	[GBUF_OFF+0],AX
		MOV	[GBUF_OFF+2],AX

		DEC	AX
		MOV	[ATTSET_NOMR],AX	;－１で初期化
		MOV	[ATTSET_SRCH],AX

		MOV	[FMAXLINE],MAXLINE

		MOV	AX,[FILELEN+0]		;メモリ獲得
		MOV	DX,[FILELEN+2]
		MOV	BX,DX
		INC	BX
		DIV	BX
		SHR	AX,4
		CMP	AX,0FF0H
		JB	@@7
		MOV	AX,0FF0H
@@7:		ADD	AX,15
		MOV	DX,AX
		ALLOCMEM AX
		JNC	ENOUGH_MEM

		CMP	BX,15
		JB	NOTE_MEM
		MOV	DX,BX
		ALLOCMEM BX
		JNC	ENOUGH_MEM

NOTE_MEM:	CALL	POP_VECT
		CALL	POPTVRAM
		PUTS	<AOF NOMEM_MES>
		RETURN	1

ENOUGH_MEM:	MOV	[HEAP],AX
		SHL	DX,4
		SUB	DX,200
		MOV	[HEAPSIZE],DX
		SHR	DX,1
		MOV	[GBUF_DIV2],DX
		MOV	[TRUE_READ],0
		MOV	[GBUF_FLAG],0

		MOV	DX,200			;マウス座標セット
		MOV	AX,4
		MUSINT
		CALL	KEYFLUSH

		RET
	ENDP

;************************************************
	PROC	CLOSE_FILE	NEAR		;ファイルをクローズ・その他諸々
		FUNCN	CLOSE_FILE

	 	H_CLOSE	[HANDLE]
		PUSH	ES
		FREEMEM	[HEAP]
		POP	ES

		MOV	BX,[FN]
		SHL	BX,1
		ADD	BX,AOF NOWTOPLINE
		MOV	AX,[NL]
		MOV	[BX],AX

		RET
	ENDP

;************************************************
	PROC	S_BACKLINE_SEL	NEAR		;一行戻り（表示無し）
		FUNCN	S_BACKLINE_SEL

		MOV	[SS_ROLL],-2
		CALL	VSYNCWAIT
		DEC	[NL]
		CALL	ATTSET
		CALL	PUTSTAT
		RET

	ENDP

;************************************************
	PROC	PUT_SEL_WAKU	NEAR		;ファイル選択の外枠
		FUNCN	PUT_SEL_WAKU

		VSETC	AL
		VCLL	0
		PUSH	ES
		MOV	CX,0A200H
		MOV	ES,CX

		MOV	CX,[FILELINE]
		MOV	DI,1*80*2
@@1:		MOV	[ES:DI],AL
		MOV	[ES:DI+79*2],AL
		ADD	DI,80*2
		LOOP	@@1

		POP	ES
		MOV	AX,[FILELINE]
		INC	AX
		VCLL	AX

		VSETC	V_WHITE
		RET
	ENDP

;************************************************
	PROC	GET_FILE	NEAR		;ファイル名を抽出
		FUNCN	GET_FILE

		PUSH	DI
		MOV	DI,SI
@@1:		MOV	AL,[SI]
		INC	SI
		TEST	AL,AL
		JZ	@@Q
		CMP	AL,'\'
		JE	@@2
		CMP	AL,'/'
		JE	@@2
		CMP	AL,':'
		JNE	@@1

@@2:		MOV	DI,SI
		JMP	SHORT @@1

@@Q:		MOV	SI,DI
		POP	DI
		RET
	ENDP

;************************************************
	PROC	PUT_SEL_FILES			;ファイルを一覧
		FUNCN	PUT_SEL_FILES

		MOV	CX,[FILELINE]
		XOR	AX,AX

@@L:		MOV	DI,AOF MYBUF
		PUSH	CX
		MOV	CX,6

@@P:		PUSH	AX
		CMP	AX,[FILES]
		JNAE	@@2
		MOV	BX,DI
		JMP	SHORT @@3

@@2:		MOV	SI,AX
		SHL	SI,FNAMEBIT
		ADD	SI,AOF FNAME
		CALL	GET_FILE

		MOV	BX,DI
		STRCPY	DI,SI
@@3:		ADD	BX,13
@@1:		MOV	[BYTE DI],' '
		INC	DI
		CMP	DI,BX
		JB	@@1
		POP	AX
		INC	AX
		LOOP	@@P

		DEC	DI
		MOV	[BYTE DI],NULL
		POP	CX
		PUSH	AX
		MOV	AX,[FILELINE]
		SUB	AX,CX
		INC	AX
		VPUTS	1,AL,<AOF MYBUF>
		POP	AX
		LOOP	@@L
		RET
	ENDP

;************************************************
	PROC	PUT_SEL_CURSOR	NEAR		;カーソル表示
		FUNCN	PUT_SEL_CURSOR

		PUSH	AX

		MOV	AX,[FN]
		MOV	BL,6
		DIV	BL
		XOR	BX,BX
		MOV	BL,AL
		MOV	AL,AH
		MOV	CL,13
		MUL	CL
		INC	AX
		INC	BX
		SHL	BX,4
		ADD	AX,BX
		SHL	BX,2
		ADD	AX,BX
		SHL	AX,1
		MOV	DI,AX

		PUSH	ES
		MOV	AX,0A200H
		MOV	ES,AX
		MOV	AX,DX
		MOV	CX,12
		CLD
	REP	STOSW
		POP	ES

		POP	AX
		RET
	ENDP

;************************************************
	PROC	SEL_FILE	NEAR		;ファイル選択
		FUNCN	SEL_FILE

		CMP	[FILES],1
		JBE	@@Q

		MOV	AX,[FILES]
		ADD	AX,5
		MOV	BL,6
		DIV	BL
		XOR	AH,AH
		MOV	[FILELINE],AX
		MOV	CX,AX
		INC	CX
		INC	CX
@@DOWN:		PUSH	CX
		CALL	S_BACKLINE_SEL
		POP	CX
		LOOP	@@DOWN

		MOV	AX,[FILELINE]
		INC	AX
		INC	AX
		ADD	[NL],AX

		MOV	AL,V_CYAN + V_REV
		CALL	PUT_SEL_WAKU
		CALL	PUT_SEL_FILES

@@L:		MOV	DX,200
		MOV	AX,4
		MUSINT			;真ん中に戻す
		MOV	DL,V_YELLOW + V_REV
		CALL	PUT_SEL_CURSOR
		CALL	BGETC
		MOV	DL,V_WHITE
		CALL	PUT_SEL_CURSOR

		CMP	AX,001BH	;[ESC]
		JE	@@OK
		CMP	AX,1C0DH	;[RET]
		JE	@@OK

@@1:		CMP	AX,3E00H	;[CLR]
		JE	@@NX
		CMP	AX,3C00H	;[-->]
		JNE	@@2
@@NX:		MOV	AX,[FN]
		INC	AX
		CMP	AX,[FILES]
		JNB	@@L
		MOV	AX,[FN]
		INC	AX
		JMP	@@HYOUJI

@@2:		CMP	AX,0AE00H	;\[CLR]
		JE	@@BK
		CMP	AX,3B00H	;[<--]
		JNE	@@3
@@BK:		MOV	AX,[FN]
		CMP	AX,0
		JE	@@L
		MOV	AX,[FN]
		DEC	AX
		JMP	@@HYOUJI

@@3:		CMP	AX,3D00H	;[DOWN]
		JNE	@@4
		MOV	AX,[FN]
		ADD	AX,6
		CMP	AX,[FILES]
		JGE	@@L
		MOV	AX,[FN]
		ADD	AX,6
		JMP	@@HYOUJI

@@4:		CMP	AX,3A00H	;[UP]
		JNE	@@L
		CMP	[FN],6
		JB	@@L
		MOV	AX,[FN]
		SUB	AX,6
		JMP	@@HYOUJI

@@HYOUJI:	PUSH	AX
		CALL	CLOSE_FILE
		POP	AX
		MOV	[FN],AX
		CALL	OPEN_FILE

		MOV	CX,[LINES]
		SUB	CX,[FILELINE]
		DEC	CX
		DEC	CX
		MOV	AX,[FILELINE]
		INC	AX
		INC	AX
		MOV	DX,[NL]
@@5:
		PUSH	DX
		PUSH	CX
		PUSH	AX
		CALL	READLINE
		POSH	AX
		VCLL	AX
		VPUTS	AH,AL,<AOF MYBUF>
		POP	AX
		POP	CX
		POP	DX
		INC	AX
		INC	DX
		LOOP	@@5
		MOV	AX,[FILELINE]
		INC	AX
		INC	AX
		SUB	[NL],AX
		PUSH	AX
		CALL	ATTSET
		CALL	PUTSTAT
		POP	AX
		ADD	[NL],AX
		JMP	@@L

@@OK:		MOV	AX,[FILELINE]
		INC	AX
		INC	AX
		SUB	[NL],AX

		MOV	CX,[FILELINE]
@@CL:		VCLL	CX
		LOOP	@@CL
		MOV	AL,V_WHITE
		CALL	PUT_SEL_WAKU
		CALL	ATTSET
		MOV	CX,[FILELINE]
		INC	CX
		INC	CX
@@UP:		PUSH	CX
		MOV	AL,[DEF_SPEED]
		CALL	S_NEXTLINE
		POP	CX
		LOOP	@@UP

@@Q:		RET
	ENDP

;************************************************
	PROC	CHILD	NEAR			;子プロセス（シェル）
		FUNCN	CHILD

		CALL	CLOSE_FILE
		CALL	POP_VECT
		CALL	POPTVRAM
		CALL	GETCWD

		MOV	DX,AOF CMDPATH
		MOV	BX,AOF PARABLK

		MOV	AX,[PSP]
		MOV	[WORD CMDLINE+0],0D00H

		MOV	[WORD BX+ 2],AOF CMDLINE
		MOV	[BX+ 4],DS
		MOV	[WORD BX+ 6],5CH
		MOV	[BX+ 8],AX
		MOV	[WORD BX+10],6CH
		MOV	[BX+12],AX

		MOV	AX,04B00H
		DOSINT

		CALL	SETCWD
		CALL	PUSHTVRAM
		CALL	PUSH_VECT

		RET
	ENDP

;************************************************
	PROC	C_EDITOR	NEAR		;エディタの起動
		FUNCN	C_EDITOR

		MOV	SI,AOF EDITOR
		MOV	DI,AOF CMDLINE+1+2
		MOV	[WORD CMDLINE+1],'C/'

@@1:		MOV	AL,[SI]
		INC	SI
		CMP	AL,NULL
		JE	@@2
		CMP	AL,'&'
		JNE	@@3

		MOV	AL,[SI]
		INC	SI
		CMP	AL,NULL
		JE	@@2

		TOUPPER AL

		CMP	AL,'&'	;'&'そのもの
		JE	@@3

		CMP	AL,'T'	;タブ数
		JNE	@@_1
		MOV	AX,[TABSIZE]
		ADD	AL,'0'
		JMP	@@3

@@_1:		CMP	AL,'F'	;ファイル名
		JNE	@@_2
		PUSH	SI
		STRCPY	DI,[NOW_FNAME]
		POP	SI
		JMP	@@4

@@_2:		CMP	AL,'P'	;表示行
		JNE	@@_3
		MOV	[LFLAG],0
		JMP	SHORT @@OFS

@@_3:		CMP	AL,'L'	;論理行
		JNE	@@_4

		MOV	[LFLAG],1

@@OFS:		MOV	DX,[NL]
		MOV	AL,[SI]
		INC	SI
		CMP	AL,NULL
		JE	@@2
		CMP	AL,'+'
		JE	@@OFS2
		CMP	AL,'-'
		JNE	@@4
@@OFS2:		MOV	AH,AL

		MOV	AL,[SI]
		INC	SI
		CMP	AL,NULL
		JE	@@2
		TOUPPER	AL
		CMP	AL,'0'
		JB	@@4
		CMP	AL,'9'
		JA	@@@A
		SUB	AL,'0'
		JMP	SHORT @@@0
@@@A:		CMP	AL,'A'
		JB	@@4
		CMP	AL,'Z'
		JA	@@4
		SUB	AL,'A'-10

@@@0:		CMP	AH,'+'
		JE	@@@1
				;minus
		XOR	AH,AH
		SUB	DX,AX
		JMP	SHORT @@@2
@@@1:		XOR	AH,AH
		ADD	DX,AX

@@@2:		CMP	[BYTE LFLAG],0
		JE	@@@25
		MOV	AX,DX
		MOV	DX,AOF WAIT_MES
		PUSH	AX
		CALL	PUTMES
		POP	AX
		PUSH	CX
		PUSH	SI
		PUSH	DI
		MOV	CX,1
		MOV	DX,CX
		MOV	[BYTE TABCFLAG],0
@@L:		CMP	DX,AX
		JGE	@@LQ
		PUSH	AX
		PUSH	CX
		PUSH	DX
		CALL	READLINE
		POP	DX
		POP	CX
		POP	AX
		ADD	CX,[ITISLF]
		INC	DX
		JMP	SHORT @@L

@@LQ:		MOV	[BYTE TABCFLAG],1
		MOV	DX,CX
		POP	DI
		POP	SI
		POP	CX

@@@25:		TEST	DX,8000H
		JZ	@@@3	;負なら
		MOV	DX,1
@@@3:		PUSH	DI
		WTODEC	DX,<AOF MYBUF>
		MOV	[BYTE DI],NULL
		POP	DI
		PUSH	SI
		SUPCPY	DI,<AOF MYBUF>
		POP	SI
@@_4:
		JMP	SHORT @@4

@@3:		MOV	[DI],AL
		INC	DI

@@4:		JMP	@@1

@@2:		MOV	[BYTE DI],NULL
		STRLEN	<AOF CMDLINE+1>
		MOV	[BYTE DI],0DH
		MOV	[CMDLINE],CL

		CALL	CLOSE_FILE
		CALL	POP_VECT
		CALL	GETCWD

		MOV	DX,AOF CMDPATH
		MOV	BX,AOF PARABLK

		MOV	AX,[PSP]

		MOV	[WORD BX+ 2],AOF CMDLINE
		MOV	[BX+ 4],DS
		MOV	[WORD BX+ 6],5CH
		MOV	[BX+ 8],AX
		MOV	[WORD BX+10],6CH
		MOV	[BX+12],AX

		MOV	AX,04B00H
		DOSINT

		CALL	SETCWD
		CALL	PUSH_VECT

		RET
	ENDP

;************************************************
	PROC	GETCWD	NEAR			;現在のディレクトリを得る
		FUNCN	GETCWD

		MOV	DI,AOF FWORK

		MOV	AH,19H
		DOSINT
		MOV	DL,AL
		INC	DL
		ADD	AL,'A'
		MOV	[DI],AL
		INC	DI
		MOV	[WORD DI],'\:'
		INC	DI
		INC	DI
		MOV	AH,47H
		MOV	SI,DI
		DOSINT

		RET
	ENDP

;************************************************
	PROC	SETCWD	NEAR			;指定のディレクトリに移動
		FUNCN	SETCWD

		MOV	DI,AOF FWORK

		MOV	AH,0EH
		MOV	DL,[DI]
		SUB	DL,'A'
		DOSINT
		MOV	AH,3BH
		MOV	DX,DI
		DOSINT

		RET
	ENDP

;************************************************
	PROC	DO_CLIP	NEAR			;クリップ　メッセージ表示もする
		FUNCN	DO_CLIP

		STRCPY	<AOF CMDLINE>,<AOF CLIP>
		CMP	[CLIPFLAG],2
		JNE	@@SNAO_OPEN

		STRCHR	<AOF CMDLINE>,'.'
		TEST	DI,DI
		JZ	@@@Q
		MOV	[BYTE DI+4],NULL
		INC	DI
		XOR	DX,DX

@@S:		BTODEC	DL,DI
		H_CREATE_NEW <AOF CMDLINE>
		JNC	@@SS
		CMP	AX,50H
		JNE	DISK_ERROR
		CMP	DL,255
		JE	@@@Q
		INC	DL
		JMP	SHORT @@S

@@SS:		MOV	BX,AX
		JMP	SHORT @@WT

@@SNAO_OPEN:	F_SEARCH <AOF CMDLINE>
		JC	@@CRE
		H_OPEN	<AOF CMDLINE>,00100001B
		JC	DISK_ERROR
		MOV	BX,AX
		XOR	CX,CX
		DEC	CX
		MOV	DX,CX
		FSEEK	BX,CX,DX,SEEK_END  ;最後－１に移動（一番最後はＥＯＦ）
		JMP	SHORT @@WT

@@CRE:		H_CREATE <AOF CMDLINE>
		JC	DISK_ERROR
		MOV	BX,AX

@@WT:		STRCPY	<AOF MYBUF>,[NOW_FNAME]
		MOV	[BYTE DI],' '
		INC	DI
		MOV	SI,AOF MYBUF+77
		MOV	AL,'-'
@@L1:		MOV	[DI],AL
		INC	DI
		CMP	DI,SI
		JBE	@@L1
		MOV	[WORD DI],0A0DH
		H_WRITE	BX,<AOF MYBUF>,80

		MOV	DX,[NL]
		MOV	CX,[LINES]
		DEC	CX
		DEC	CX

@@L2:		PUSH	CX
		PUSH	DX
		PUSH	BX
		CALL	READLINE
		POP	BX
		STRLEN	<AOF MYBUF>
		TEST	CX,CX
		JNZ	@@L3
		MOV	[WORD MYBUF],0A0DH
		INC	CX
		INC	CX
@@L3:		H_WRITE	BX,<AOF MYBUF>,CX
		POP	DX
		POP	CX
		INC	DX
		LOOP	@@L2

		CMP	[ITISLF],0
		JNE	@@Q
		MOV	[WORD MYBUF],0A0DH
		H_WRITE	BX,<AOF MYBUF>,2

@@Q:		MOV	[BYTE MYBUF],EOF
		H_WRITE	BX,<AOF MYBUF>,1
		H_CLOSE	BX

		MOV	DI,AOF MYBUF
		STRCPY	DI,<AOF CLIP_MES>
		STRCPY	DI,<AOF CMDLINE>
		STRCPY	DI,<AOF KOKKA_MES>
		MOV	DX,AOF MYBUF
		CALL	PUTMES

@@@Q:		CALL	KEYFLUSH
		RET
	ENDP

;************************************************
	PROC	READLINE	NEAR		;DX行目を読み込みMYBUFへ
		FUNCN	READLINE

		MOV	[LINE],DX

		CMP	DX,[READMAXLINE]	;すでに読んでるか
		JBE	ALREADY_READ

NOT_READED:	MOV	DX,[NOWBYTE+2]
		MOV	AX,[NOWBYTE+0]
		CALL	F_READ

		MOV	BX,AOF EOF_MES2
		CMP	[LINE],MAXLINE
		JA	@@A

		MOV	AX,[NOWBYTE+2]		;続きがあるか
		CMP	AX,[FILELEN+2]
		JB	MORE_EXIST
		MOV	AX,[NOWBYTE+0]
		CMP	AX,[FILELEN+0]
		JB	MORE_EXIST

		MOV	BX,AOF EOF_MES
@@A:		MOV	AX,[FMAXLINE]		;おしまい
		INC	AX
		CMP	[LINE],AX
		JNBE	OVER_EOF

		MOV	DX,[READMAXLINE]	;ポインタセット
		MOV	DI,AOF LP_BYTE
		MOV	SI,AOF FILELEN
		CLD
		MOVSW
		MOVSW
		CALL	SET_LP

		STRCPY	<AOF MYBUF>,BX ;ちょうど終わりの行
		JMP	SHORT NO_MORE

OVER_EOF:	XOR	AX,AX			;最後の行より後ろ
		MOV	[MYBUF+0],AL

		MOV	DX,[LINE]		;ポインタセット
		MOV	DI,AOF LP_BYTE
		MOV	SI,AOF FILELEN
		CLD
		MOVSW
		MOVSW
		CALL	SET_LP

NO_MORE:	MOV	AX,[READMAXLINE]	;最大行セット
		MOV	[FMAXLINE],AX
		MOV	[ITISLF],1
		RET

MORE_EXIST:	MOV	DX,[READMAXLINE]	;続きがある
		MOV	DI,AOF LP_BYTE
		MOV	SI,AOF NOWBYTE
		CLD
		MOVSW
		MOVSW
		CALL	SET_LP

		MOV	BX,AOF MYBUF
		CALL	CUTLINE 		;80文字切りだし
		ADD	[NOWBYTE+0],AX
		ADC	[NOWBYTE+2],0

		INC	[READMAXLINE]		;目的行まで読んだか
		MOV	CX,[READMAXLINE]
		CMP	CX,[LINE]
		JB	NOT_READED
		JMP	SHORT READLINE_OK

ALREADY_READ:	MOV	DX,[LINE]		;ポインタ移動
		CALL	GET_LP

		MOV	DX,[LP_BYTE+2]
		MOV	AX,[LP_BYTE]
		CALL	F_READ

		MOV	BX,AOF MYBUF
		CALL	CUTLINE 		;切り出し

READLINE_OK:	MOV	BX,AOF MYBUF		;タブ変換
		CMP	[BYTE TABCFLAG],0
		JNE	TABCONV 		;Equal CALL TABCONV;RET;
		RET

	ENDP

;************************************************
	PROC	CUTLINE	NEAR			;１行(８０文字)切り出し
		FUNCN	CUTLINE			;BX <= 文字列,BXは破壊されます

		XOR	DI,DI			;DI:何バイト目か
		MOV	CX,DI			;CX:何文字目か(0から)
		MOV	[ITISLF],DI	;ITISLF <= 0

CUT_LOOP:	ISKANJI [BX+DI]			;2バイト文字か？
		JNC	CUT_ANK

		MOV	DL,[BX+DI]		;全角か？
		CMP	DL,86H
		JNBE	CUT_ZEN
		CMP	DL,85H
		JB	CUT_ZEN

		CMP	DL,86H
		JNE	CUT_HAN
		MOV	DL,[BX+DI+1]
		CMP	DL,9EH
		JBE	CUT_HAN

CUT_ZEN:	INC	CX			;全角

CUT_HAN:	INC	DI			;半角２バイト
		JMP	SHORT CHK_EOS

CUT_ANK:	MOV	DL,[BX+DI]
		CMP	DL,TAB			;タブか？
		JNE	CUT_NOTAB

		MOV	AX,[TABSIZE]
		ADD	CX,AX
		DEC	AX
		NOT	AX
		AND	CX,AX

		DEC	CX
		JMP	SHORT CHK_EOS

CUT_NOTAB:
		CMP	DL,LF
		JE	CUT_LF
		CMP	DL,CR
		JNE	CHK_EOS

CUT_CR:						;ＣＲの時
		CMP	[BYTE BX+DI+1],LF
		JNE	CUT_LF
		INC	DI

CHK_EOS:					;ループ判断
		INC	CX
CUT_LF:
		MOV	DL,[BX+DI]
		INC	DI
		CMP	DL,LF
		JE	CUT_END2
		CMP	DL,CR
		JE	CUT_END2
		CMP	CX,80
		JAE	CUT_END
		CMP	DI,[LEN]
		JNB	CUT_END2

		JMP	CUT_LOOP

CUT_END2:	MOV	[ITISLF],1
CUT_END:	CMP	CX,80			;切り出し完了
		JNA	SET_NULL

		DEC	DI
		DEC	DI
		DEC	CX
		DEC	CX

		JMP	SHORT CUT_END

SET_NULL:					;行末にNULLを付ける
		DEC	BX			;ＤＩだと思うだろうがＢＸでよい
		MOV	DL,[BYTE BX+DI]
;		CMP	DL,CR
;		JE	SET_NULL
;		CMP	DL,LF
;		JE	SET_NULL
		CMP	DL,EOF			;いらない文字をここにかく
		JE	SET_NULL

		INC	BX
		MOV	[BYTE BX+DI],NULL

		MOV	AX,DI		; AX <= 文字列のバイト数
		RET

	ENDP

;************************************************
	PROC	BGETC	NEAR			;１文字入力
		FUNCN	BGETC

SCAN:		CALL	KBHIT
		TEST	BH,BH
		JNZ	KEY_IN

		CMP	[MOUSE],0
		JE	@@3

		CALL	MGET
		TEST	AX,AX			;次のファイルか
		JZ	@@1
		CALL	MOUSEOFFWAIT
		MOV	AX,3E00H
		JMP	BGET_EXIT
@@1:		TEST	BX,BX			;前のファイルか
		JZ	@@2
		CALL	MOUSEOFFWAIT
		MOV	AX,0AE00H
		JMP	BGET_EXIT

@@2:		MOV	AL,16
		CMP	DX,33
		JBE	M_BACK
		CMP	DX,366
		JNBE	M_NEXT

		MOV	AL,8
		CMP	DX,66
		JBE	M_BACK
		CMP	DX,333
		JNBE	M_NEXT

		MOV	AL,4
		CMP	DX,99
		JBE	M_BACK
		CMP	DX,300
		JNBE	M_NEXT

		MOV	AL,2
		CMP	DX,132
		JBE	M_BACK
		CMP	DX,267
		JNBE	M_NEXT

		MOV	AL,1
		CMP	DX,165
		JBE	M_BACK
		CMP	DX,234
		JNBE	M_NEXT
@@3:
		INT	28H
		CALL	VSYNCWAIT
		JMP	SCAN

M_NEXT:		MOV	AH,0FFH
		JMP	SHORT BGET_EXIT

M_BACK:		MOV	AH,0FEH
		JMP	SHORT BGET_EXIT

KEY_IN:		MOV	DX,200
		MOV	AX,4
		MUSINT

		CALL	GETCH

		CALL	KEYFLUSH

BGET_EXIT:	RET

	ENDP

;************************************************
	PROC	F_READ	NEAR			;バッファリング読み込み
		FUNCN	F_READ

		PUSH	DX
		PUSH	AX

		CMP	[GBUF_FLAG],0
		JNZ	@@4

		MOV	CX,[TRUE_READ]
		MOV	BX,[HEAPSIZE]
		CMP	CX,BX
		JB	@@TR1
		MOV	CX,[GBUF_OFF+2]
		MOV	BX,[GBUF_OFF+0]
		MOV	[GBUF_FLAG],1
		JMP	NEXTGBUF

@@TR1:		CMP	AX,[TRUE_READ]
		JAE	@@FW
		MOV	SI,AX
		JMP	@@HIT

@@FW:		MOV	BX,[HEAPSIZE]
		CMP	CX,4608
		JBE	@@@3
		ADD	CX,8192
		JMP	SHORT @@@4
@@@3:		ADD	CX,1536
@@@4:		JC	@@@2
		CMP	CX,BX
		JNA	@@@1
@@@2:		MOV	CX,BX
@@@1:		PUSH	AX
		PUSH	DX
		PUSH	CX
		FSEEK	[HANDLE],0,[TRUE_READ],SEEK_SET
		MOV	DX,[TRUE_READ]
		POSH	CX
		SUB	CX,[TRUE_READ]
		ADD	CX,180
		MOV	BX,[HANDLE]
		PUSH	DS
		MOV	AX,[HEAP]
		MOV	DS,AX
		MOV	AH,3FH
		DOSINT
		POP	DS
		POP	CX
		POP	DX
		POP	AX
		JC	DISK_ERROR
		MOV	[TRUE_READ],CX
		MOV	SI,AX
		JMP	@@HIT

@@4:		MOV	CX,[GBUF_OFF+2]
		MOV	BX,[GBUF_OFF+0]
		SUB	AX,BX
		SBB	DX,CX
		JB	BACKGBUF

		TEST	DX,DX
		JNZ	NEXTGBUF
		CMP	AX,[HEAPSIZE]
		JNBE	NEXTGBUF

		POP	AX
		POP	DX
		JMP	GBUF_HIT

BACKGBUF:	POP	AX
		POP	DX

		PUSH	DX
		PUSH	AX

		SUB	BX,[HEAPSIZE]
		SBB	CX,0
		JNB	@@1
		XOR	BX,BX
		XOR	CX,CX

@@1:		SUB	AX,BX
		SBB	DX,CX
		JB	BACKGBUF
		JMP	SHORT G_SEEK

NEXTGBUF:	POP	AX
		POP	DX

		PUSH	DX
		PUSH	AX

		SUB	AX,BX
		SBB	DX,CX

		TEST	DX,DX
		JNZ	@@5
		SUB	AX,[HEAPSIZE]
		JB	G_SEEK

@@5:		ADD	BX,[HEAPSIZE]
		ADC	CX,0
		JMP	SHORT NEXTGBUF

G_SEEK:		POP	AX
		POP	DX

		MOV	[GBUF_OFF+2],CX
		MOV	[GBUF_OFF+0],BX

		PUSH	DX
		PUSH	AX
		MOV	DX,BX
		FSEEK	[HANDLE],CX,DX,SEEK_SET
		JC	DISK_ERROR

		PUSH	DS
		XOR	DX,DX
		MOV	CX,[HEAPSIZE]
		ADD	CX,180
		MOV	BX,[HANDLE]
		MOV	AX,[HEAP]
		MOV	DS,AX
		MOV	AH,3FH
		DOSINT
		POP	DS
		JC	DISK_ERROR

		POP	AX
		POP	DX

GBUF_HIT:	PUSH	DX
		PUSH	AX

		MOV	BX,[GBUF_OFF+0]
		SUB	AX,BX
		MOV	SI,AX

@@HIT:		MOV	CX,161
		MOV	DI,AOF MYBUF

		PUSH	DS
		MOV	AX,[HEAP]
		MOV	DS,AX
		CLD
	REP	MOVSB
		POP	DS

		POP	AX
		POP	DX

		MOV	BX,[FILELEN+0]
		MOV	CX,[FILELEN+2]
		SUB	CX,DX
		SBB	BX,AX

		TEST	CX,CX
		JNZ	@@3
		CMP	BX,161
		JBE	@@2
@@3:		MOV	BX,161

@@2:		MOV	[LEN],BX

		MOV	DI,AOF MYBUF-1
@@6:		CMP	BX,0
		JE	@@7
		DEC	BX
		INC	DI
		CMP	[BYTE DI],NULL
		JNE	@@6
		MOV	[BYTE DI],' '
		JMP	SHORT @@6

;@@@5:		CMP	[BYTE DI],05CH
;		JNE	@@6
;		MOV	[BYTE DI],0FCH

@@7:		RET

	ENDP

;************************************************
	PROC	TABCONV	NEAR			;タブをスペースに変換
		FUNCN	TABCONV

		XOR	AX,AX

TABC_LOOP:	MOV	DL,[BX]
		CMP	DL,TAB
		JNE	NO_TAB

		PUSH	AX
		MOV	DX,[TABSIZE]
		DEC	DX
		AND	AX,DX
		SUB	DX,AX
		MOV	AX,DX

		MOV	DI,BX
		INC	DI
		CALL	SFTSTR

		INC	AX
		SETMEM	BX,AX,' '
		POP	AX

		JMP	@@N

NO_TAB:		MOV	DI,AX
		ISKANJI	DL
		MOV	AX,DI
		JNC	@@N

		MOV	DL,[BX]
		CMP	DL,86H
		JNBE	@@ZEN
		CMP	DL,85H
		JB	@@ZEN

		CMP	DL,86H
		JNE	@@HAN
		MOV	DL,[BX+1]
		CMP	DL,9EH
		JBE	@@HAN

@@ZEN:		INC	AX
@@HAN:		INC	BX

@@N:		INC	AX
		MOV	DL,[BX]
		INC	BX
		TEST	DL,DL
		JNZ	TABC_LOOP

		RET

	ENDP

;************************************************
	PROC	GETFN	NEAR			;ファイル名の補完
		FUNCN	GETFN

		MOV	DI,SI
@@2:		MOV	AL,[SI]
		INC	SI
		TEST	AL,AL
		JZ	@@3
		ISKANJI	AL
		JNC	@@5
		MOV	AL,[SI]
		TEST	AL,AL
		JZ	@@3
		INC	SI
		JMP	SHORT @@2
@@5:		CMP	AL,':'
		JE	@@4
		CMP	AL,'\'
		JNE	@@1
@@4:		MOV	DI,SI
@@1:		JMP	SHORT @@2

@@3:		CMP	[BYTE DI],'.'
		JE	HOKAN
		CMP	[BYTE DI],NULL
		JNE	GETFN_EXIT
HOKAN:
		MOV	BX,DI
		MOV	AX,1
		CALL	SFTSTR
		MOV	[BYTE BX],'*'

GETFN_EXIT:	RET
	ENDP

;************************************************
	PROC	SFTSTR	NEAR			;文字列をずらす
		FUNCN	SFTSTR

		PUSH	AX

		MOV	CX,0FFFFH
		XOR	AL,AL
		CLD
	REPNE	SCASB
		NEG	CX

		POSH	AX
		MOV	SI,DI
		ADD	DI,AX
		STD
	REP	MOVSB

		POP	AX
		RET

	ENDP

;************************************************
	PROC	STRWCMP	NEAR			;ワイルドカード付き比較
		FUNCN	STRWCMP

		PUSH	SI
		PUSH	DI

DO2:		MOV	AL,[DI]
		CMP	AL,'*'
		JE	@EQ
		CMP	AL,'?'
		JE	NEXT2

		TOUPPER AL
		MOV	AH,AL

		TOUPPER [SI]
		CMP	AH,AL
		JNE	NOEQ

NEXT2:		CMP	[BYTE DI],NULL
		JE	@@1
		CMP	[BYTE SI],NULL
		JE	NOEQ
		INC	SI
		INC	DI
		JMP	SHORT DO2

@@1:		CMP	[BYTE SI],NULL
		JE	@EQ

NOEQ:		MOV	AL,0FFH
		JMP	SHORT @1

@EQ:		XOR	AL,AL
@1:		POP	DI
		POP	SI
		RET
	ENDP

;************************************************
	PROC	GET_LP	NEAR			;DX 行目のファイルポインタ取得
		FUNCN	GET_LP

		CMP	DX,1
		JG	@@NB
		MOV	[LP_BYTE  ],0
		MOV	[LP_BYTE+2],0
		RET

@@NB:		PUSHA
		MOV	DI,AOF _LINE_POINTER
		MOV	CX,DX
		DEC	CX
		MOV	BX,[LP_NOW_LINE]
		CMP	CX,BX
		JE	@@Q
		JB	@@B

		XOR	AX,AX
@@L1:		MOV	AL,[DI+BX]
		INC	BX
		ADD	[LP_NOW_BYTE],AX
		ADC	[LP_NOW_BYTE+2],0
		CMP	CX,BX
		JA	@@L1

		JMP	SHORT @@Q
@@B:					;現在位置より前
		XOR	AX,AX
@@L2:		DEC	BX
		MOV	AL,[DI+BX]
		SUB	[LP_NOW_BYTE],AX
		SBB	[LP_NOW_BYTE+2],0
		CMP	CX,BX
		JB	@@L2

@@Q:		MOV	[LP_NOW_LINE],BX
		MOV	DI,AOF LP_BYTE
		MOV	SI,AOF LP_NOW_BYTE
		CLD
		MOVSW
		MOVSW
		POPA
		RET
	ENDP

;************************************************
	PROC	SET_LP	NEAR			;DX 行目のファイルポインタ設定
		FUNCN	SET_LP

		PUSH	AX
		PUSH	BX
		PUSH	DX

		MOV	AX,[LP_BYTE]
		MOV	BX,[LP_BYTE+2]

		CALL	GET_LP
		XCHG	DX,BX
		ADD	BX,AOF _LINE_POINTER - 1

		SUB	AX,[LP_BYTE]
;		SBB	DX,[LP_BYTE+2]	;事実上無意味。

		MOV	[BX],AL
		POP	DX
		POP	BX
		POP	AX
		RET
	ENDP

;************************************************
	PROC	FSTRNICMP	NEAR		;DS:SI と ES:DI をNI比較
		FUNCN	FSTRNICMP

		XOR	AL,AL
		TEST	CX,CX
		JZ	@@EXIT

@@LOOP:		MOV	AL,[DS:SI]
		TOUPPER	AL
		XCHG	AL,AH
		MOV	AL,[ES:DI]
		TOUPPER	AL
		XCHG	AL,AH
		SUB	AL,AH
		JNE	@@EXIT
		MOV	AL,[DS:SI]
		TEST	AL,AL
		JZ	@@EXIT
		INC	SI
		INC	DI
		LOOP	@@LOOP
		XOR	AL,AL

@@EXIT:		RET
	ENDP

;************************************************
	PROC	SETCOMSPEC	NEAR		;環境変数COMSPECの処理
		FUNCN	SETCOMSPEC

		PUSH	AX
		PUSH	DS

		MOV	DS,AX
		MOV	AX,[02CH]	;AX <= ENV SEGMENT
		MOV	DS,AX
		XOR	SI,SI		;DS:SI <= ENV POINTER

		MOV	DI,AOF COMSPECNAME

@@1:		PUSH	DI
		MOV	CX,8
		CALL	FSTRNICMP
		POP	DI
		TEST	AL,AL
		JZ	COMSPEC_ATTA

@@2:		INC	SI
		CMP	[BYTE SI-1],NULL
		JNE	@@2

		CMP	[BYTE SI],NULL
		JE	@@Q
		JMP	SHORT @@1

COMSPEC_ATTA:	MOV	DI,AOF CMDPATH

@@4:		MOV	AL,[SI]
		MOV	[ES:DI],AL
		INC	SI
		INC	DI
		TEST	AL,AL
		JNE	@@4

@@Q:		POP	DS
		POP	AX
		RET
	ENDP

;************************************************
	PROC	SETMOSTENV	NEAR		;環境変数MOSTの処理
		FUNCN	SETMOSTENV

		PUSH	DS
		MOV	DS,AX
		MOV	AX,[02CH]	;AX <= ENV SEGMENT
		MOV	DS,AX
		XOR	SI,SI		;DS:SI <= ENV POINTER

		MOV	DI,AOF ENVNAME

@@1:		PUSH	DI
		MOV	CX,5
		CALL	FSTRNICMP
		POP	DI
		TEST	AL,AL
		JZ	MOST_ATTA

@@2:		INC	SI
		CMP	[BYTE SI-1],NULL
		JNE	@@2

		CMP	[BYTE SI],NULL
		JE	@@Q
		JMP	SHORT @@1

MOST_ATTA:	MOV	AL,[SI]
		INC	SI
		CMP	AL,NULL
		JE	@@Q
		CMP	AL,' '
		JE	MOST_ATTA
		CMP	AL,TAB
		JE	MOST_ATTA
		DEC	SI

		PUSH	SI
		MOV	DI,AOF ENVNAME1		;"EXT:"
		MOV	CX,4
		CALL	FSTRNICMP
		POP	SI
		TEST	AL,AL
		JNZ	@@3
		ADD	SI,4
		MOV	DI,AOF MOSTEXT
		MOV	BX,AOF MOSTEXTNUM
		CALL	GETEXT
		JMP	SHORT @@E

@@3:		PUSH	SI
		MOV	DI,AOF ENVNAME2		;"TAB4:"
		MOV	CX,5
		CALL	FSTRNICMP
		POP	SI
		TEST	AL,AL
		JNZ	@@4
		ADD	SI,5
		MOV	DI,AOF MOSTEXT2
		MOV	BX,AOF MOSTEXTNUM2
		CALL	GETEXT
		JMP	SHORT @@E

@@4:		PUSH	SI
		MOV	DI,AOF ENVNAME3		;"SPD:"
		MOV	CX,4
		CALL	FSTRNICMP
		POP	SI
		TEST	AL,AL
		JNZ	@@5
		ADD	SI,4
		CALL	GETSPD
		JMP	SHORT @@E

@@5:		PUSH	SI
		MOV	DI,AOF ENVNAME4		;"EDIT:"
		MOV	CX,5
		CALL	FSTRNICMP
		POP	SI
		TEST	AL,AL
		JNZ	@@6
		ADD	SI,5
		CALL	GETEDIT
		JMP	SHORT @@E

@@6:		PUSH	SI
		MOV	DI,AOF ENVNAME5		;"CLIP:"
		MOV	CX,5
		CALL	FSTRNICMP
		POP	SI
		TEST	AL,AL
		JNZ	@@7
		ADD	SI,5
		CALL	GETCLIP
		JMP	SHORT @@E
@@7:
		PUSH	SI
		MOV	DI,AOF ENVNAME6		;"FLAG:"
		MOV	CX,5
		CALL	FSTRNICMP
		POP	SI
		TEST	AL,AL
		JNZ	@@8
		ADD	SI,5
		CALL	GETFLAG
;		JMP	SHORT @@E
@@8:
@@E:
		MOV	AL,[SI]
		CMP	AL,NULL
		JE	@@Q
		CMP	AL,' '
		JE	MOST_ATTA
		CMP	AL,TAB
		JE	MOST_ATTA
		INC	SI
		CMP	AL,';'
		JE	MOST_ATTA
		JMP	SHORT @@E

@@Q:		POP	DS
		RET
	ENDP

;************************************************
	PROC	GETEXT	NEAR			;環境から拡張子を得る
		FUNCN	GETEXT

		MOV	[WORD ES:BX],0

@@1:		MOV	AL,[SI]
		INC	SI
		CMP	AL,' '
		JE	@@1
		CMP	AL,TAB
		JE	@@1
		CMP	AL,'.'
		JNE	@@Q

		CMP	[WORD ES:BX],10	;多すぎるブロック
		JAE	@@1

		INC	[WORD ES:BX]
		XOR	BP,BP

@@2:		MOV	AL,[SI]
		INC	SI
		CMP	AL,NULL
		JE	@@CUTEND
		CMP	AL,' '
		JE	@@CUTEND
		CMP	AL,TAB
		JE	@@CUTEND
		CMP	AL,';'
		JE	@@CUTEND
		CMP	AL,'.'
		JE	@@CUTEND

		MOV	[ES:DI+BP],AL
		INC	BP
		CMP	BP,4
		JB	SHORT @@2

@@CUTEND:	DEC	SI
		MOV	[BYTE ES:DI+BP],NULL
		ADD	DI,4
		JMP	SHORT @@1

@@Q:		DEC	SI
		RET
	ENDP

;************************************************
	PROC	GETSPD	NEAR			;速度の初期化
		FUNCN	GETSPD

		MOV	DI,AOF DEF_SPEED
		MOV	CX,4

@@1:		MOV	DL,[SI]
		INC	SI
		CMP	DL,NULL
		JE	@@Q
		CMP	DL,' '
		JE	@@1
		CMP	DL,TAB
		JE	@@1
		CMP	DL,';'
		JE	@@Q
		CMP	DL,'1'
		JB	@@1
		CMP	DL,'5'
		JA	@@1

		SUB	DL,'1'
		MOV	AX,1
		PUSH	CX
		MOV	CX,DX
		SHL	AX,CL
		POP	CX
		MOV	[ES:DI],AL
		INC	DI

		LOOP	@@1

@@Q:		DEC	SI
		RET
	ENDP

;************************************************
	PROC	GETEDIT	NEAR			;エディタ名の取得
		FUNCN	GETEDIT

		MOV	DI,AOF EDITOR
@@0:		MOV	AL,[SI]
		CMP	AL,';'
		JE	@@Q
		INC	SI
		CMP	AL,' '
		JE	@@0
		CMP	AL,TAB
		JE	@@0

		DEC	SI
@@1:		MOV	AL,[SI]
		CMP	AL,NULL
		JE	@@Q
		CMP	AL,';'
		JE	@@Q
		INC	SI
		MOV	[ES:DI],AL
		INC	DI
		JMP	SHORT @@1

@@Q:		CMP	DI,AOF EDITOR
		JBE	@@X
		DEC	DI
		CMP	[BYTE ES:DI],' '
		JE	@@Q
		CMP	[BYTE ES:DI],TAB
		JE	@@Q
		INC	DI
@@X:		MOV	[BYTE ES:DI],NULL
		RET
	ENDP

;************************************************
	PROC	GETCLIP	NEAR			;クリップファイル名の取得
		FUNCN	GETCLIP

		MOV	[ES:CLIPFLAG],1	;１はアペンドモード
		MOV	DI,AOF CLIP
@@0:		MOV	AL,[SI]
		CMP	AL,';'
		JE	@@Q
		INC	SI
		CMP	AL,' '
		JE	@@0
		CMP	AL,TAB
		JE	@@0

		DEC	SI
@@1:		MOV	AL,[SI]
		CMP	AL,NULL
		JE	@@Q
		CMP	AL,';'
		JE	@@Q
		INC	SI
		MOV	[ES:DI],AL
		INC	DI
		JMP	SHORT @@1

@@Q:		CMP	DI,AOF EDITOR
		JBE	@@X
		DEC	DI
		CMP	[BYTE ES:DI],' '
		JE	@@Q
		CMP	[BYTE ES:DI],TAB
		JE	@@Q
		INC	DI
@@X:		MOV	[BYTE ES:DI],NULL
		PUSH	DS
		PUSH	ES
		POP	DS
		STRCHR	<AOF CLIP>,'.'
		POP	DS
		TEST	DI,DI
		JZ	@@@Q
		INC	DI
		CMP	[BYTE ES:DI],'*'
		JNE	@@@Q
		INC	[ES:CLIPFLAG]	;[CLIPFLAG] := 2 振り分けモード
@@@Q:		RET
	ENDP

;************************************************
	PROC	GETFLAG	NEAR			;各種フラグを取得
		FUNCN	GETFLAG

		CALL	NEXTCHAR
		CMP	DL,'1'
		JB	@@2
		CMP	DL,'9'
		JA	@@2

		SUB	DL,'1'
		XOR	AX,AX
		MOV	AL,DL
		MOV	[ES:POS_LINE],AX
@@2:
		CALL	NEXTCHAR
		CMP	DL,'1'
		JB	@@3
		CMP	DL,'2'
		JA	@@3

		SUB	DL,'0'
		MOV	[ES:INITIAL_LINES],DL
@@3:
		CALL	NEXTCHAR
		CMP	DL,'Y'
		JE	@@4
		CMP	DL,'y'
		JNE	@@5
@@4:
		MOV	[ES:EDIT_QUIT],ON
@@5:
		CALL	NEXTCHAR
		CMP	DL,'F'
		JE	@@6
		CMP	DL,'f'
		JNE	@@7
@@6:
		MOV	[ES:ROLL_FULL],ON
@@7:
		DEC	SI
		RET

NEXTCHAR:	MOV	DL,[SI]
		INC	SI
		CMP	DL,NULL
		JE	@@Q
		CMP	DL,' '
		JE	NEXTCHAR
		CMP	DL,TAB
		JE	NEXTCHAR
		CMP	DL,';'
		JE	@@Q

		RET

@@Q:		POP	AX	;ＩＰをＰＯＰ
		DEC	SI
		RET
	ENDP

;************************************************
	PROC	MOUSEOFFWAIT	NEAR		;マウスのボタンを離すのを待つ
		FUNCN	MOUSEOFFWAIT

MUSWAIT:	CALL	MGET
		TEST	AX,AX
		JNZ	MUSWAIT
		TEST	BX,BX
		JNZ	MUSWAIT
		RET
	ENDP

;************************************************
	PROC	MGET	NEAR			;マウスのボタンと座標を得る
		FUNCN	MGET

		MGETXY
		RET
	ENDP

;************************************************
	PROC	VSYNCWAIT	NEAR		;1/60秒待つ

;		PUSH	AX

		MOV	[SYNC_FLAG],1
		OUT	64H,AL

@@1:		CMP	[SYNC_FLAG],1
		JE	@@1

;		POP	AX
		RET
	ENDP

;************************************************
	PROC	GETCH	NEAR			;一文字入力
		FUNCN	GETCH

	IF KEYBIOS_CALL
		XOR	AX,AX
		INT	18H
		RET
	ELSE
@@2:		CALL	KBHIT
		TEST	BH,BH
		JZ	@@2

		CLI
		PUSH	BX
		PUSH	ES
		PUSH	DI
		XOR	AX,AX
		MOV	ES,AX
		MOV	BX,0524H
		MOV	DI,[ES:BX]
		MOV	AX,[ES:DI]
		DEC	[BYTE ES:0528H]
		INC	[WORD ES:BX]
		INC	[WORD ES:BX]
		CMP	[WORD ES:BX],0520H
		JNA	@@1
		MOV	[WORD ES:BX],0502H
@@1:		POP	DI
		POP	ES
		POP	BX
		STI
		RET
	ENDIF
	ENDP

;************************************************
	PROC	KBHIT	NEAR			;キーが押されたか
		FUNCN	KBHIT

	IF KEYBIOS_CALL
		MOV	AH,1
		INT	18H
		RET
	ELSE
		PUSH	DS
		XOR	BX,BX
		MOV	DS,BX
		MOV	BH,[0528H]
		POP	DS
		RET
	ENDIF
	ENDP

;************************************************
	PROC	GETSFT	NEAR			;シフトキー状態
		FUNCN	GETSFT

	IF KEYBIOS_CALL
		MOV	AH,2
		INT	18H
		RET
	ELSE
		PUSH	DS
		XOR	AX,AX
		MOV	DS,AX
		MOV	AL,[053AH]
		POP	DS
		RET
	ENDIF
	ENDP

;************************************************
	PROC	CHKTBL_DOWN	NEAR
		FUNCN	CHKTBL_DOWN

	IF KEYBIOS_CALL
		MOV	AX,0407H
		INT	18H
		TEST	AH,32
		JZ	@@1
		RET
@@1:
		MOV	AX,0405H
		INT	18H
		TEST	AH,4
		RET
	ELSE
		PUSH	DS
		PUSH	0
		POP	DS
		TEST	[BYTE DS:052AH+7],32
		JZ	@@1
		POP	DS
		RET
@@1:		TEST	[BYTE DS:052AH+5],4
		POP	DS
		RET
	ENDIF
	ENDP

;************************************************
	PROC	CHKTBL_UP	NEAR
		FUNCN	CHKTBL_UP

	IF KEYBIOS_CALL
		MOV	AX,0407H
		INT	18H
		TEST	AH,4
		JZ	@@1
		RET
@@1:
		MOV	AX,0402H
		INT	18H
		TEST	AH,4
		RET
	ELSE
		PUSH	DS
		PUSH	0
		POP	DS
		TEST	[BYTE DS:052AH+7],4
		JZ	@@1
		POP	DS
		RET
@@1:		TEST	[BYTE DS:052AH+2],4
		POP	DS
		RET
	ENDIF
	ENDP

;************************************************
	PROC	KEYFLUSH	NEAR		;キーバッファのクリア
		FUNCN	KEYFLUSH

	IF KEYBIOS_CALL
		PUSH	AX
@@1:
		MOV	AH,1
		INT	18H
		TEST	BH,BH
		JZ	@@Q
		XOR	AX,AX
		INT	18H
		JMP	SHORT @@1
@@Q:
		POP	AX
		RET
	ELSE
		PUSH	ES
		PUSH	AX

		XOR	AX,AX
		MOV	ES,AX
		CLI
		MOV	[ES:0528H],AL
		MOV	AX,[ES:0524H]
		MOV	[ES:0526H],AX
		STI

		POP	AX
		POP	ES
		RET
	ENDIF
	ENDP

;************************************************
	PROC	ROLLUP	NEAR			;ウィンドウロールアップ

		PUSH	DS
		PUSH	ES

		SETSEG	DS,0A000H
		PUSH	DS
		POP	ES

		MOV	SI,1*80 *2
		XOR	DI,DI
		MOV	CX,29*80
		CLD
	REP	MOVSW				;キャラクタ領域

		MOV	DI,29*80 *2		;最下行クリア
		MOV	CX,80
		MOV	AX,0020H
	REP	STOSW

		POP	ES
		POP	DS

		RET

	ENDP

;************************************************
	PROC	ROLLDOWN	NEAR		;ウィンドウロールダウン

		PUSH	DS
		PUSH	ES

		SETSEG	DS,0A000H
		PUSH	DS
		POP	ES

		MOV	SI,(29*80-1) *2
		MOV	DI,(30*80-1) *2
		MOV	CX,29*80
		STD
	REP	MOVSW

		XOR	DI,DI			;最上行クリア
		MOV	CX,80
		MOV	AX,0020H
		CLD
	REP	STOSW

		POP	ES
		POP	DS

		RET

	ENDP

;************************************************
	PROC	RDN1	NEAR			;苦肉の策①

		PUSH	DS
		SETSEG	DS,0A000H

		XOR	SI,SI
		MOV	DI,AOF RDN_BUF
		MOV	CX,29*80
		CLD
	REP	MOVSW

		POP	DS
		RET

	ENDP

;************************************************
	PROC	RDN2	NEAR			;苦肉の策②

		PUSH	ES
		SETSEG	ES,0A000H

		XOR	DI,DI			;最上行クリア
		MOV	CX,80
		MOV	AX,0020H
		CLD
	REP	STOSW

		MOV	SI,AOF RDN_BUF
		MOV	DI,80*2
		MOV	CX,29*80
	REP	MOVSW

		POP	ES
		RET

	ENDP

;************************************************
	PROC	SEARCH_ATT	NEAR		;検索結果に印をつける
		FUNCN	SEARCH_ATT

		PUSH	AX
		MOV	DX,[SEARCHLINE]
		SUB	DX,[NL]

		MOV	DI,DX
		SHL	DX,4
		SHL	DI,6
		ADD	DI,DX
		ADD	DI,[SEARCHBYTE]
		SHL	DI,1
		MOV	[ATTSET_SRCH],DI
		MOV	BX,AOF SEARCHWORD+2
		STRLEN	BX
		MOV	SI,[SEARCHBYTE]

		POP	AX
		PUSH	ES
		MOV	DX,0A200H
		MOV	ES,DX

@@8:		CMP	[BYTE BX],TAB
		JNE	@@6

		PUSH	CX
		MOV	DX,[TABSIZE]
		MOV	CX,DX
		DEC	DX
		AND	DX,SI
		SUB	CX,DX
		MOV	DX,[TABSIZE]
		ADD	SI,DX
		DEC	DX
		NOT	DX
		AND	SI,DX
@@7:		MOV	[ES:DI],AX
		INC	DI
		INC	DI
		LOOP	@@7
		POP	CX
		JMP	SHORT @@9

@@6:		MOV	[ES:DI],AX
		INC	SI
		INC	DI
		INC	DI

@@9:		INC	BX
		DEC	CX
		JNZ	@@8
		POP	ES

		RET
	ENDP

;************************************************
	PROC	ATTSET	NEAR
		FUNCN	ATTSET

		PUSH	ES
		PUSH	AX
		PUSH	CX
		PUSH	DX

		MOV	AX,0A000H + (2000H SHR 4)
		MOV	ES,AX
		CLD

		MOV	DI,[ATTSET_NOMR]
		CMP	DI,-1
		JE	NOMR_END

		MOV	CX,31
		MOV	AX,V_WHITE
	REP	STOSW
		MOV	[ATTSET_NOMR],-1

NOMR_END:	MOV	DX,[FMAXLINE]
		SUB	DX,[NL]
		INC	DX
		CMP	DX,[LINES]
		JAE	ATTSET_EXIT

		MOV	DI,DX
		SHL	DX,5
		SHL	DI,7
		ADD	DI,DX
		MOV	[ATTSET_NOMR],DI
		MOV	CX,31
		MOV	AX,V_GREEN + V_REV
	REP	STOSW

ATTSET_EXIT:	POP	DX
		POP	CX
		POP	AX
		POP	ES
		RET

	ENDP

;************************************************
	PROC	VCLS30	NEAR			;３０行対応画面クリア
		FUNCN	VCLS30

		PUSH	AX
		PUSH	CX
		PUSH	DI
		PUSH	ES

		MOV	AX,0A000H
		PUSH	AX
		POP	ES

		MOV	CX,30*80
		XOR	DI,DI
		MOV	AX,20H
		CLD
	REP	STOSW

		MOV	CX,30*80
		MOV	DI,2000H
		MOV	AX,0E1H
	REP	STOSW

		POP	ES
		POP	DI
		POP	CX
		POP	AX

		RET
	ENDP

;************************************************
	PROC	PUSHTVRAM	NEAR		;VRAMを退避

		PUTS	<AOF ESC_1>

		PUSH	DS
		SETSEG	DS,0A000H

		XOR	SI,SI
		MOV	DI,AOF TVRAM_STACK
		MOV	CX,TVRAMSIZE
		CLD
	REP	MOVSW
		MOV	SI,2000H
		MOV	CX,TVRAMSIZE
	REP	MOVSW
		POP	DS

		RET

	ENDP

;************************************************
	PROC	POPTVRAM	NEAR		;VRAMを復帰

		PUSH	ES
		SETSEG	ES,0A000H

		MOV	SI,AOF TVRAM_STACK
		XOR	DI,DI
		MOV	CX,TVRAMSIZE
		CLD
	REP	MOVSW
		MOV	DI,2000H
		MOV	CX,TVRAMSIZE
	REP	MOVSW
		POP	ES

		PUTS	<AOF ESC_2>
		PUSH	DS
		SETSEG	DS,60H
		MOV	AL,[112H]
		CMP	[110H],AL
		JBE	@@4
		MOV	[BYTE 110H],AL
@@4:		POP	DS

		RET
	ENDP

;************************************************
	PROC	SETLINES	NEAR		;行数をセット AX<=LINES

		MOV	[LINES],AX
		PUSH	AX
		SUB	AX,4
		MOV	[LINES2],AX
		POSH	AX
		DEC	AX

		CMP	[ROLL_FULL],0
		JNE	@@1
		SHR	AX,1
@@1:
		MOV	[LINEH],AX
		POP	AX
		MOV	[RASTER],20
		CMP	AX,20
		JE	@@Q
		MOV	[RASTER],16
@@Q:
		CALL	SPLIT
		RET
	ENDP

;************************************************
	PROC	SPLIT	NEAR			;画面をスプリット
		FUNCN	SPLIT

		VGETC
		PUSH	AX

		MOV	AX,0A01H
		CMP	[LINES],20
		JE	@@3
		DEC	AX
@@3:		INT	18H

		MOV	AH,0FH			;画面分割
		MOV	BX,DS
		MOV	CX,AOF CRT_ST2
		CMP	[LINES],20
		JE	@@4
		MOV	CX,AOF CRT_ST
@@4:		MOV	DX,0002
		INT	18H

		VSETC	<V_CYAN + V_REV>
		VCLL	30

		XOR	AL,AL			;スムーススクロール
		OUT	78H,AL
		MOV	AX,[LINES]
		DEC	AX
		DEC	AX
		OUT	7AH,AL
		XOR	AL,AL
		OUT	76H,AL

		POP	AX
		VSETC	AL

		RET
	ENDP

;************************************************
	PROC	PUSH_VECT	NEAR		;各種ベクタ等の退避

		PUTS	<AOF CUR_OFF>
		MOV	[SS_COUNT],0
		MOV	[SS_ROLL],0
		MOV	[SYNC_FLAG],0

		PUSH	ES
;		MOV	AX,3506H		;ＳＴＯＰキーベクタ
;		DOSINT
;		MOV	[WORD CS:OLD_STOP_VECT+0],BX
;		MOV	[WORD CS:OLD_STOP_VECT+2],ES
		MOV	AX,3518H		;ＣＲＴベクタ
		DOSINT
		MOV	[WORD CS:OLD_18H_VECT+0],BX
		MOV	[WORD CS:OLD_18H_VECT+2],ES
		MOV	AX,350AH		;ＶＳＹＮＣベクタ
		DOSINT
		MOV	[WORD CS:OLD_CRTV_VECT+0],BX
		MOV	[WORD CS:OLD_CRTV_VECT+2],ES
		POP	ES

		PUSH	DS			;ＣＴＲＬ－Ｃベクタ
		PUSH	CS
		POP	DS
		MOV	DX,AOF DEAD_IRET
		MOV	AX,2523H
		DOSINT
;		MOV	AX,2506H		;ＳＴＯＰキー
;		DOSINT
		MOV	AX,2518H
		MOV	DX,AOF INT18H		;新しいＣＲＴベクタ
		DOSINT
		MOV	AX,250AH
		MOV	DX,AOF VSYNC		;新しいＶＳＹＮＣベクタ
		DOSINT
		POP	DS

		IN	AL,2			;割り込みマスク
		MOV	[OLD_INT_MASK],AL
		AND	AL,0FBH
		OUT	2,AL
		OUT	64H,AL

		PUSH	DS			;ＢＩＯＳワーク
		SETSEG	DS,60H			;ファンクションキーを消すのも
		MOV	AX,[0111H]		;同時にやる
		POP	DS

		MOV	[FKEY_FLAG],OFF

		TEST	AL,AL
		JZ	@@1
		MOV	[FKEY_FLAG],ON
		PUTS	<AOF FKEY_OFF>
		INC	AH
@@1:		XOR	AL,AL
		XCHG	AH,AL
		INC	AX	;AX<=Screen lines

		MOV	[OLD_LINES],AL
		CMP	AL,30
		JE	@@2
		CMP	[INITIAL_LINES],0
		JE	@@2
		MOV	AX,20
		CMP	[INITIAL_LINES],2
		JE	@@2
		MOV	AX,25
@@2:
		CALL	SETLINES

		MOV	AX,3300H		;ＣＴＲＬ－Ｃチェック
		DOSINT
		MOV	[OLD_BREAK],DL

		MOV	AX,3301H
		XOR	DL,DL
		DOSINT

		RET

	ENDP

;************************************************
	PROC	POP_VECT	NEAR		;各種ベクタの復帰

		MOV	AL,[OLD_INT_MASK]	;割り込みマスクを戻す
		OUT	2,AL

		PUSH	DS			;ＳＴＯＰキーベクタ
;		MOV	DX,[WORD CS:OLD_STOP_VECT+0]
;		MOV	DS,[WORD CS:OLD_STOP_VECT+2]
;		MOV	AX,2506H
;		DOSINT
		MOV	DX,[WORD CS:OLD_18H_VECT+0]
		MOV	DS,[WORD CS:OLD_18H_VECT+2]
		MOV	AX,2518H
		DOSINT
		MOV	DX,[WORD CS:OLD_CRTV_VECT+0]
		MOV	DS,[WORD CS:OLD_CRTV_VECT+2]
		MOV	AX,250AH
		DOSINT
		POP	DS

		XOR	DX,DX			;ＣＲＴモード
		MOV	AH,0EH
		INT	18H

		MOV	AL,[OLD_LINES]
		CMP	AL,20
		JNE	@@1
		CMP	[LINES],20
		JE	@@2
		PUTS	<AOF SET_L20>
		JMP	SHORT @@2
@@1:
		CMP	[LINES],20
		JNE	@@2
		PUTS	<AOF SET_L25>
@@2:
		MOV	AX,3301H		;ＣＴＲＬ－Ｃチェック
		MOV	DL,[OLD_BREAK]
		DOSINT

		CMP	[FKEY_FLAG],OFF
		JE	@@3
		PUTS	<AOF FKEY_ON>
@@3:
		PUTS	<AOF CUR_ON>

		RET

	ENDP

;************************************************
	PROC	VSYNC	NEAR;(int)		;ＶＳＹＮＣ割り込みルーチン

		STI

	IF VSYNC_MARK
		PUSH	DS
		PUSH	0A000H
		POP	DS
		MOV	[WORD DS:150],'V'
		MOV	[WORD DS:152],'S'
		MOV	[WORD DS:154],'Y'
		MOV	[WORD DS:156],'N'
		MOV	[WORD DS:158],'C'
		POP	DS
	ENDIF

		PUSH	DS
		PUSH	AX
		SETSEG	DS,DGROUP

		MOV	AL,[SS_COUNT]
		OUT	76H,AL
		MOV	AX,[SS_ROLL]
		CMP	AX,0
		JE	@@2
		CMP	AX,1
		JNE	@@3

		PUSH	CX
		PUSH	SI
		PUSH	DI
		CALL	ROLLUP
		POP	DI
		POP	SI
		POP	CX
		JMP	SHORT @@2

@@3:		CMP	AX,-1
		JNE	@@4

		PUSH	CX
		PUSH	SI
		PUSH	DI
		CALL	RDN2
		POP	DI
		POP	SI
		POP	CX

@@4:		CMP	AX,-2
		JNE	@@2

		PUSH	CX
		PUSH	SI
		PUSH	DI
		CALL	ROLLDOWN
		POP	DI
		POP	SI
		POP	CX
@@2:
		MOV	[SS_ROLL],0
		MOV	[SYNC_FLAG],0

;		MOV	AL,20H		;ＥＯＩ
;		OUT	0,AL
		OUT	64H,AL		;ＶＳＹＮＣリセット

		POP	AX
		POP	DS

		JMP	[CS:OLD_CRTV_VECT]
	ENDP

;************************************************
	PROC	INT18H	NEAR;(int)		;ＣＲＴ－ＢＩＯＳフックルーチン
		PUSHF
		CALL	[CS:OLD_18H_VECT]
		OUT	64H,AL		;ＢＩＯＳのバグじゃないの？こんなの
		IRET
	ENDP

;************************************************
DEAD_IRET:
		IRET
;************************************************
DISK_ERROR:	CALL	POP_VECT
		CALL	POPTVRAM
		PUTS	<AOF D_ERR_MES>
		RETURN	1
;************************************************
;OLD_STOP_VECT	DD	0			;特殊ベクタ退避用
OLD_CRTV_VECT	DD	0
OLD_18H_VECT	DD	0

	ENDS

;************************************************
	DEFS	DATA

CRT_ST		DW	0,24,4801,1
CRT_ST2		DW	0,19,4801,1
FILES		DW	0
FN		DW	0
MOSTEXTNUM	DW	2 ; (.DOC , .TXT)
MOSTEXTNUM2	DW	2 ; (.C , .H)
HELP_MES	DW	AOF H_1 ,AOF H_2 ,AOF H_3 ,AOF H_4 ,AOF H_5
		DW	AOF H_6 ,AOF H_7 ,AOF H_8 ,AOF H_9 ,AOF H_10
		DW	AOF H_11,AOF H_12,AOF H_13,AOF H_14,AOF H_15
		DW	AOF H_16,AOF H_17,AOF H_18,AOF H_19
PARABLK		DW	0,0,0,0,0,0,0
POS_LINE	DW	0
QFLAG		DW	0
TABCFLAG	DB	1
DEF_SPEED	DB	4,16,2,8
CMDPATH		DB	"COMMAND.COM",80 DUP(NULL)
CLIPFLAG	DB	0

CREDIT		DB	"ｱ｣｣｣｣｣｣｣｣｣｣｣｣｣｣ｵ",CR,LF
		DB	"･ファイル ページャー ＭＯＳＴ･",CR,LF
		DB	"･     by Ken/ichiro(OPA)     ･",CR,LF
		DB	"ｹ｣｣｣｣｣｣｣｣｣｣｣｣｣｣ｽ",CR,LF
		DB	"		Version ",_VERSION,CR,LF
NULLP		DB	NULL
USASE_MES	DB	"起動法：",CR,LF
		DB	"	A>MOST [ファイル名] ...",CR,LF
		DB	"	A>MOST <ファイル名",CR,LF
		DB	"	A>foo | MOST",CR,LF
		DB	"HELPでキー操作一覧が表示されます。",CR,LF,NULL
CANMOUSE_MES	DB	"マウスが使用可能です。",CR,LF,NULL

NOMEM_MES	DB	"空きメモリが足りません。",CR,LF,NULL
NO_EXIST_MES	DB	"見つかりません:[",NULL
CANNOT_OPEN_MES	DB	"開けません:[",NULL
CLIP_MES	DB	"クリップしました:[",NULL
KOKKA_MES	DB	"]",CR,LF,NULL
D_ERR_MES	DB	"ディスクアクセス中にエラーが発生しました。",CR,LF,NULL

H_1		DB	"   ^X,↓        ｦｦ  次の行            "
		DB	"   ^E,↑        ｦｦ  前の行",NULL
H_2		DB	"   SHIFT+↑,↓  ｦｦ  高速スクロール",NULL
H_3		DB	"   ^↑,^W,^↓,^Zｦｦ  連続スクロール",NULL
H_4		DB	"   ^C,ROLLUP    ｦｦ  次の半ページ      "
		DB	"   ^R,ROLLDOWN  ｦｦ  前の半ページ",NULL
H_5		DB	"   [SPACE]      ｦｦ  次のページ",NULL
H_6		DB	"   ^QR          ｦｦ  ファイル先頭へ    "
		DB	"   ^QC          ｦｦ  ファイル末端へ",NULL
H_7		DB	"   ^J[0-9]      ｦｦ  現在位置をマーク  "
		DB	"   [F3][0-9]    ｦｦ  マークへジャンプ",NULL
H_8		DB	"   ^^           ｦｦ  前方検索          "
		DB	"   ^\           ｦｦ  後方検索",NULL
H_9		DB	"   [F5],\[F5]   ｦｦ  次を検索          "
		DB	"   [TAB]        ｦｦ  タブ間隔(4/8)変更",NULL
H_10		DB	"   (\^)[INS]    ｦｦ  加速              "
		DB	"   (\^)[DEL]    ｦｦ  減速",NULL
H_11		DB	"   [HOME]       ｦｦ  次のファイル      "
		DB	"   \[HOME]      ｦｦ  前のファイル",NULL
H_12		DB	"   [F2]         ｦｦ  ファイル選択      "
		DB	"   [F6],P       ｦｦ  クリップ",NULL
H_13		DB	"   [HELP]       ｦｦ  ヘルプ            "
		DB	"   ^G           ｦｦ  画面モード切替",NULL
H_14		DB	"   \[RET]       ｦｦ  エディタの起動    "
		DB	"   ^[RET],^QO   ｦｦ  子プロセス",NULL
H_15		DB	"   [ESC],[RET]  ｦｦ  終了",NULL

H_17		DB	"   マウス下     ｦｦ  次の行            "
		DB	"   マウス上     ｦｦ  まえの行",NULL
H_18		DB	"   左ボタン     ｦｦ  次のファイル      "
		DB	"   右ボタン     ｦｦ  前のファイル",NULL

LABEL	H_16	BYTE
LABEL	H_19	BYTE
		DB	NULL

MARKPROM	DB	"  Mark(0-9)>",NULL
JUMPPROM	DB	"  Jump(0-9)>",NULL
PROMPT		DB	ESCC,"[>5l",ESCC,"[46m",NULL
PROMPT1		DB	ESCC,"=7  FIND>",NULL
PROMPT2		DB	ESCC,"=2  FIND>",NULL
PROMPT3		DB	ESCC,"==  FIND>",NULL
PROM_2		DB	ESCC,"[>5h",ESCC,"[m",NULL
PREHELP		DB	"キー操作一覧：",NULL
WAIT_MES	DB	" **** MOST:wait... ****",NULL
CANT_FIND_MES	DB	" **** 見つかりません。****",NULL
WAITING_MES	DB	" **** 何かキーを押して下さい。****",NULL
EOF_MES		DB	"〓〓〓〓〓  No more  〓〓〓〓〓",CR,LF,NULL
EOF_MES2	DB	"▽▽▽▽▽  M O R E  ▽▽▽▽▽",CR,LF,NULL

CUR_OFF		DB	ESCC,"[>5h",NULL
CUR_ON		DB	ESCC,"[>5l",NULL
FKEY_OFF	DB	ESCC,"[>1h",NULL
FKEY_ON		DB	ESCC,"[>1l",NULL
ESC_1		DB	ESCC,"[s",NULL
ESC_2		DB	ESCC,"[u",NULL
CON_PATH	DB	"CON",NULL
LINE_MES	DB	" line: ",NULL
BYTE_MES	DB	" bytes(",NULL
CENT_MES	DB	"%) ----",NULL
COMSPECNAME	DB	"COMSPEC="
EDIT_QUIT	DB	OFF	;エディタ起動後終了するか
ROLL_FULL	DB	OFF	;[ROLLUP/DOWN]での１画面移動
ENVNAME		DB	"MOST="
ENVNAME1	DB	"EXT:"
ENVNAME2	DB	"TAB4:"
ENVNAME3	DB	"SPD:"
ENVNAME4	DB	"EDIT:"
ENVNAME5	DB	"CLIP:"
ENVNAME6	DB	"FLAG:"
INITIAL_LINES	DB	0	;0:そのまま,1:２５行,2:２０行
MOSTEXT		DB	"DOC",NULL,"TXT",NULL,50 DUP(?)
MOSTEXT2	DB	"C",3 DUP(NULL),"H",3 DUP(NULL),50 DUP(?)
SET_L20		DB	ESCC,"[>3h",NULL
SET_L25		DB	ESCC,"[>3l",NULL

	ENDS

;************************************************
	DEFS		BSS

FILELEN		DW	?,?			;ほんとはDDなのさ
GBUF_OFF	DW	?,?
NOWBYTE		DW	?,?
LP_BYTE		DW	?,?
LP_NOW_BYTE	DW	?,?

ATTSET_NOMR	DW	?
ATTSET_SRCH	DW	?
FILE_C		DW	?
FILE_D		DW	?
FILELINE	DW	?
FMAXLINE	DW	?
GBUF_DIV2	DW	?
HANDLE		DW	?
HEAP		DW	?
HEAPSIZE	DW	?
INKEY		DW	?
ITISLF		DW	?
LEN		DW	?
LINE		DW	?
LINEH		DW	?
LINES		DW	?
LINES2		DW	?
LP_NOW_LINE	DW	?
MARKBUF		DW	10*2 DUP(?)
MOUSE		DW	?
NL		DW	?
NOW_FNAME	DW	?
NOWTOPLINE	DW	MAXFILES DUP(?)
PSP		DW	?
RASTER		DW	?
RDN_BUF		DW	1000H DUP(?)
READMAXLINE	DW	?
SEARCHBYTE	DW	?
SEARCHDIR	DW	?
SEARCHLINE	DW	?
SS_ROLL		DW	?
TABSIZE		DW	?
TRUE_READ	DW	?
TVRAM_STACK	DW	(TVRAMSIZE*2) DUP(?)

CLIP		DB	80 DUP(?)
CMDLINE		DB	128 DUP(?)
DTA		DB	50 DUP(?)
EDITOR		DB	80 DUP(?)
FNAME		DB	(MAXFILES * FNAMESIZE) DUP(?)
FWORK		DB	80 DUP(?)
FKEY_FLAG	DB	?
GBUF_FLAG	DB	?
LFLAG		DB	?
_LINE_POINTER	DB	(MAXLINE + 4) DUP(?)
MESBUF		DB	81 DUP(?)
MYBUF		DB	170 DUP(?)
OLD_BREAK	DB	?
OLD_LINES	DB	?
OLD_INT_MASK	DB	?
SEARCHWORD	DB	80 DUP(?)
SS_STEP		DB	?
SS_COUNT	DB	?
S_FLAG1		DB	?
SYNC_FLAG	DB	?
TABSIZES	DB	MAXFILES DUP(?)

	ENDS
;************************************************
STACK_SIZE	200

	PEND
	END	START
